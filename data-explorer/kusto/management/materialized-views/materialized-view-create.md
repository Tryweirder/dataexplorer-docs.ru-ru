---
title: Создание материализованных представлений в Azure обозреватель данных
description: В этой статье описывается создание материализованных представлений в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: yifats
ms.service: data-explorer
ms.topic: reference
ms.date: 08/30/2020
ms.openlocfilehash: 95f8ce19c6edb419de4fb5053a79c243e3e332c4
ms.sourcegitcommit: 608539af6ab511aa11d82c17b782641340fc8974
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 10/20/2020
ms.locfileid: "92252851"
---
# <a name="create-materialized-view"></a>.create materialized-view

[Материализованный вид](materialized-view-overview.md) — это статистический запрос к исходной таблице, представляющий одну инструкцию суммирования.

Существует два возможных способа создания материализованных представлений, отмеченных параметром « *обратная засыпку* ».

 * **Создать на основе существующих записей в исходной таблице:** 
      * Создание может занять длительное время, в зависимости от количества записей в исходной таблице. Представление не будет доступно для запросов до завершения.
      * При использовании этого параметра команда CREATE должна быть `async` отслеживаема, а выполнение можно отслеживать с помощью команды [. показ операций](../operations.md#show-operations) .

    * Отмена процесса обратной обработки возможна с помощью команды [. Отмена операции](#cancel-materialized-view-creation) .

      > [!IMPORTANT]
      > * Использование параметра «обратная засыпку» не поддерживается для данных в холодном кэше. При необходимости увеличьте период оперативного кэша для создания представления. Для этого может потребоваться горизонтальное масштабирование.    
      > * Использование параметра «обратная засыпку» может занять длительное время для больших исходных таблиц. Если при выполнении этого процесса происходит сбой, повторная попытка не будет выполнена автоматически, и требуется повторное выполнение команды Create.
    
* **Создание материализованных представлений с помощью:** 
    * Материализованный представление создается пустым и будет включать только записи, полученные после создания представления. Создание такого вида возвращается немедленно, не требует `async` , и представление будет немедленно доступно для запроса.

Для операции создания требуются разрешения [администратора базы данных](../access-control/role-based-authorization.md) . Создатель материализованных представлений станет его администратором.

## <a name="syntax"></a>Синтаксис

`.create` [`async`] `materialized-view` <br>
[ `with` `(` *PropertyName* `=` *propertyvalue* `,` ... `)` ] <br>
*ViewName* `on table` *SourceTableName* <br>
`{`<br>&nbsp;&nbsp;&nbsp;&nbsp;*Выбор*<br>`}`

## <a name="arguments"></a>Аргументы

|Аргумент|Тип|Описание
|----------------|-------|---|
|ViewName|Строка|Имя материализованных представлений. Имя представления не может конфликтовать с именами таблиц или функций в той же базе данных и должно соответствовать [правилам именования идентификаторов](../../query/schema-entities/entity-names.md#identifier-naming-rules). |
|SourceTableName|Строка|Имя исходной таблицы, в которой определено представление.|
|Запрос|Строка|Запрос материализованных представлений. Дополнительные сведения см. в разделе [Query](#query-argument).|

### <a name="query-argument"></a>Аргумент запроса

Запрос, используемый в аргументе материализованных представлений, ограничен следующими правилами.

* Аргумент запроса должен ссылаться на отдельную таблицу фактов, которая является источником материализованных представлений, включать один оператор суммирования и одну или несколько агрегатных функций, агрегированных по одной или нескольким группам по выражениям. Оператор суммирования всегда должен быть последним оператором в запросе.

* Представление — это либо `arg_max` / `arg_min` / `any` представление (эти функции могут использоваться совместно в одном представлении), либо любые другие поддерживаемые функции, но не оба в одном материализованным представлении. 
    Например, `SourceTable | summarize arg_max(Timestamp, *), count() by Id` не поддерживается. 

* Запрос не должен содержать операторы, которые зависят от оператора `now()` или `ingestion_time()` . Например, запрос не должен иметь `where Timestamp > ago(5d)` . Материализованные представления с `arg_max` / `arg_min` / `any` агрегатом не могут включать другие поддерживаемые статистические функции. Ограничьте период времени, охватываемый представлением, с помощью политики хранения материализованных представлений.

* Составные агрегаты не поддерживаются в определении материализованных представлений. Например, вместо следующего представления: `SourceTable | summarize Result=sum(Column1)/sum(Column2) by Id` Определите материализованный вид: `SourceTable | summarize a=sum(Column1), b=sum(Column2) by Id` . Во время запроса представления запустите- `ViewName | project Id, Result=a/b` . Требуемые выходные данные представления, включая вычисляемый столбец ( `a/b` ), можно инкапсулировать в [хранимую функцию](../../query/functions/user-defined-functions.md). Доступ к хранимой функции вместо доступа к материализованным представлениям напрямую.

* Запросы между кластерами и между базами данных не поддерживаются.

* Ссылки на [external_table ()](../../query/externaltablefunction.md) и [externaldata](../../query/externaldata-operator.md) не поддерживаются.

* Помимо исходной таблицы представления, она также может ссылаться на один или несколько [`dimension tables`](../../concepts/fact-and-dimension-tables.md) . Таблицы измерений должны быть явно вызваны в свойствах представления. Важно понимать поведение при соединении с таблицами измерений:

    * Записи в исходной таблице представления (таблице фактов) соотносятся только один раз. Другая задержка приема между таблицей фактов и таблицей измерения может повлиять на результаты представления.

    * **Пример**. определение представления включает внутреннее соединение с таблицей измерения. На момент материализации запись измерения не была полностью принята, но уже была принята к таблице фактов. Эта запись будет удалена из представления, и повторная обработка еще не выполняется. 

        Аналогично, если соединение является внешним соединением, запись из таблицы фактов будет обработана и добавлена к представлению со значением NULL для столбцов таблицы измерения. Записи, которые уже были добавлены (со значениями NULL) в представление, не будут обрабатываться повторно. Их значения в столбцах из таблицы измерения останутся равными null.

## <a name="properties"></a>Свойства

В предложении поддерживаются следующие `with(propertyName=propertyValue)` условия. Все свойства являются необязательными.

|Свойство|Тип|Описание |
|----------------|-------|---|
|задним числом|bool|Следует ли создать представление на основе всех записей, находящихся в данный момент в *SourceTable* ( `true` ), или создать его "from-now-on" ( `false` ). Значение по умолчанию — `false`.| 
|еффективедатетиме|DATETIME| Если указано вместе с `backfill=true` , создание только незаполненных записей с записями, полученными после даты и времени. Обратная засыпку также должна иметь значение true. Ожидает литерал DateTime, например `effectiveDateTime=datetime(2019-05-01)`|
|дименсионтаблес|Массив|Разделенный запятыми список таблиц измерений в представлении. См. [аргумент запроса](#query-argument)
|аутаупдатесчема|bool|Указывает, следует ли автоматическое обновление представления в исходной таблице. Значение по умолчанию — `false`. Этот параметр допустим только для представлений типа `arg_max(Timestamp, *)`  /  `arg_min(Timestamp, *)`  /  `any(*)` (только если аргумент Columns имеет значение `*` ). Если для этого параметра задано значение true, изменения в исходной таблице будут автоматически отражены в материализованных представлениях.
|folder|строка|Папка материализованных представлений.|
|docString|строка|Строка, в которой документально документирование материализованных представлений|

> [!WARNING]
> * Использование `autoUpdateSchema` может привести к необратимой потере данных при удалении столбцов исходной таблицы. 
> * Если в исходную таблицу внесено изменение, приводящее к изменению схемы материализованным представлением, и `autoUpdateSchema` имеет значение false, представление будет автоматически отключено. 
>    * Эта ошибка часто возникает при использовании `arg_max(Timestamp, *)` и добавлении столбцов в исходную таблицу. 
>    * Избегайте этого сбоя, определив запрос представления как `arg_max(Timestamp, Column1, Column2, ...)` или с помощью `autoUpdateSchema` параметра.
> * Если представление отключено по этим причинам, можно снова включить его после устранения проблемы с помощью команды [Enable материализованный Просмотр](materialized-view-enable-disable.md) .
>

## <a name="examples"></a>Примеры

1. Создайте пустое представление arg_max, в котором будут выматериализоваться только записи, полученные от Now:

    <!-- csl -->
    ```
    .create materialized-view ArgMax on table T
    {
        T | summarize arg_max(Timestamp, *) by User
    }
    ```
    
1. Создание материализованных представлений для ежедневных статистических выражений с помощью параметра «обратная обработка» с использованием `async` следующих средств:

    <!-- csl -->
    ```
    .create async materialized-view with (backfill=true, docString="Customer telemetry") CustomerUsage on table T
    {
        T 
        | extend Day = bin(Timestamp, 1d)
        | summarize count(), dcount(User), max(Duration) by Customer, Day 
    } 
    ```
    
1. Создание материализованных представлений с помощью обратных и `effectiveDateTime` . Представление создается на основе записей только из даты и времени:

    <!-- csl -->
    ```
    .create async materialized-view with (backfill=true, effectiveDateTime=datetime(2019-01-01)) CustomerUsage on table T 
    {
        T 
        | extend Day = bin(Timestamp, 1d)
        | summarize count(), dcount(User), max(Duration) by Customer, Day
    } 
    ```
1. Материализованные представления, которые отменяют дублирование исходной таблицы на основе столбца EventId:

    <!-- csl -->
    ```
    .create materialized-view DedupedT on table T
    {
        T
        | summarize any(*) by EventId
    }
    ```

1. Определение может содержать дополнительные операторы перед `summarize` инструкцией, если только `summarize` это последний из них:

    <!-- csl -->
    ```
    .create materialized-view CustomerUsage on table T 
    {
        T 
        | where Customer in ("Customer1", "Customer2", "CustomerN")
        | parse Url with "https://contoso.com/" Api "/" *
        | extend Month = startofmonth(Timestamp)
        | summarize count(), dcount(User), max(Duration) by Customer, Api, Month
    }
    ```

1. Материализованные представления, которые соединяются с таблицей измерения:

    <!-- csl -->
    ```
    .create materialized-view EnrichedArgMax on table T with (dimensionTables = ['DimUsers'])
    {
        T
        | lookup DimUsers on User  
        | summarize arg_max(Timestamp, *) by User 
    }
    
    .create materialized-view EnrichedArgMax on table T with (dimensionTables = ['DimUsers'])
    {
        DimUsers | project User, Age, Address
        | join kind=rightouter hint.strategy=broadcast T on User
        | summarize arg_max(Timestamp, *) by User 
    }
    ```
    

## <a name="supported-aggregation-functions"></a>Поддерживаемые агрегатные функции

Поддерживаются следующие статистические функции:

* [`count`](../../query/count-aggfunction.md)
* [`countif`](../../query/countif-aggfunction.md)
* [`dcount`](../../query/dcount-aggfunction.md)
* [`dcountif`](../../query/dcountif-aggfunction.md)
* [`min`](../../query/min-aggfunction.md)
* [`max`](../../query/max-aggfunction.md)
* [`avg`](../../query/avg-aggfunction.md)
* [`avgif`](../../query/avgif-aggfunction.md)
* [`sum`](../../query/sum-aggfunction.md)
* [`arg_max`](../../query/arg-max-aggfunction.md)
* [`arg_min`](../../query/arg-min-aggfunction.md)
* [`any`](../../query/any-aggfunction.md)
* [`hll`](../../query/hll-aggfunction.md)
* [`make_set`](../../query/makeset-aggfunction.md)
* [`make_list`](../../query/makelist-aggfunction.md)
* [`percentile`, `percentiles`](../../query/percentiles-aggfunction.md)

## <a name="performance-tips"></a>Советы по улучшению производительности

* Фильтры запросов материализованных представлений оптимизируются при фильтрации по одному из материализованных измерений представления (статистическое выражение по предложению). Если известно, что шаблон запроса часто фильтрует по какому-либо столбцу, который может быть измерением в материализованным представлении, включите его в представление. Например, для материализованных представлений, предоставляя объект, `arg_max` `ResourceId` который часто будет фильтроваться по, рекомендуется использовать `SubscriptionId` следующую рекомендацию:

    **Выполните**следующие действия.
    
    ```kusto
    .create materialized-view ArgMaxResourceId on table FactResources
    {
        FactResources | summarize arg_max(Timestamp, *) by SubscriptionId, ResouceId 
    }
    ``` 
    
    **Не делать**:
    
    ```kusto
    .create materialized-view ArgMaxResourceId on table FactResources
    {
        FactResources | summarize arg_max(Timestamp, *) by ResouceId 
    }
    ```

* Не включайте преобразования, нормализации и другие сложные вычисления, которые можно переместить в [политику обновления](../updatepolicy.md) как часть определения материализованных представлений. Вместо этого выполните все эти процессы в политике обновления и выполните агрегирование только в материализованных представлениях. Используйте этот процесс для уточняющего запроса в таблицах измерений, если применимо.

    **Выполните**следующие действия.
    
    * Политика обновления:
    
    ```kusto
    .alter-merge table Target policy update 
    @'[{"IsEnabled": true, 
        "Source": "SourceTable", 
        "Query": 
            "SourceTable 
            | extend ResourceId = strcat('subscriptions/', toupper(SubscriptionId), '/', resourceId)", 
        "IsTransactional": false}]'  
    ```
        
    * Материализованный вид:
    
    ```kusto
    .create materialized-view Usage on table Events
    {
    &nbsp;     Target 
    &nbsp;     | summarize count() by ResourceId 
    }
    ```
    
    **Не делать**:
    
    ```kusto
    .create materialized-view Usage on table SourceTable
    {
    &nbsp;     SourceTable 
    &nbsp;     | extend ResourceId = strcat('subscriptions/', toupper(SubscriptionId), '/', resourceId)
    &nbsp;     | summarize count() by ResourceId
    }
    ```

> [!NOTE]
> Если требуется оптимальная производительность запросов, но это может пожертвовать актуальностью данных, используйте [функцию materialized_view ()](../../query/materialized-view-function.md).

## <a name="limitations-on-creating-materialized-views"></a>Ограничения на создание материализованных представлений

* Невозможно создать материализованный вид:
    * Поверх другого материализованных представлений.
    * В [базах данных следующих](../../../follower.md). Базы данных следующих действий доступны только для чтения, а материализованные представления — для операций записи.  Материализованные представления, определенные в базах данных лидера, можно запрашивать из их подписчиков, как и любые другие таблицы в лидере. 
* Исходная таблица материализованных представлений:
    * Должна быть таблицей, которая принимается напрямую, либо с помощью одного из [методов приема](../../../ingest-data-overview.md#ingestion-methods-and-tools), с помощью [политики обновления](../updatepolicy.md), либо путем [приема из команд запроса](../data-ingestion/ingest-from-query.md).
        * В частности, использование [перемещения экстентов](../move-extents.md) из других таблиц в исходную таблицу материализованных представлений не поддерживается. Может произойти сбой перемещения экстентов со следующей ошибкой: `Cannot drop/move extents from/to table 'TableName' since Materialized View 'ViewName' is currently processing some of these extents` . 
    * Должна быть включена [Политика инжестионтиме](../ingestiontimepolicy.md) (по умолчанию включена).
    * Невозможно включить для приема потоковой передачи.
    * Не может быть таблицей с ограниченным доступом или таблицей с включенной защитой на уровне строк.
* [Функции курсора](../databasecursor.md#cursor-functions) нельзя использовать на основе материализованных представлений.
* Непрерывный экспорт из материализованных представлений не поддерживается.

## <a name="cancel-materialized-view-creation"></a>Отмена материализации — создание представления

Отмена процесса создания материализованных представлений при использовании `backfill` параметра. Это действие полезно, когда создание занимает слишком много времени и вы хотите прервать его выполнение.  

> [!WARNING]
> Невозможно восстановить материализованные представления после выполнения этой команды.

Процесс создания не может быть прерван немедленно. Команда Cancel сигнализирует о прекращении материализации, а создание периодически проверяет, была ли запрошена отмена. Команда Cancel ожидает максимальный период в 10 минут до тех пор, пока процесс создания материализованных представлений не будет отменен и снова сообщит об успешном завершении. Даже если отмена завершилась неудачей в течение 10 минут, а команда Cancel сообщает об ошибке, то, возможно, материализованный вид будет прерван позже в процессе создания. Команда [. Показать операции](../operations.md#show-operations) будет указывать, была ли операция отменена. `cancel operation`Команда поддерживается только для отмены создания материализованных представлений, а не для отмены каких-либо других операций.

### <a name="syntax"></a>Синтаксис

`.cancel`С идентификатором `operation` *operationId*

### <a name="properties"></a>Свойства

|Свойство|Тип|Описание
|----------------|-------|---|
|operationId|Guid|ИДЕНТИФИКАТОР операции, возвращенный командой CREATE материализованных представлений.|

### <a name="output"></a>Выходные данные

|Выходной параметр |Тип |Описание
|---|---|---
|Операции|Guid|ИДЕНТИФИКАТОР операции команды создания материализованных представлений.
|Операция|Строка|Тип операции.
|стартедон|DATETIME|Время начала операции создания.
|канцеллатионстате|строка|Одно из- `Cancelled successfully` (операция создания отменено), `Cancellation failed` (истекло время ожидания для отмены `Unknown` операции) (создание представления больше не выполняется, но не отменено этой операцией).
|ReasonPhrase|строка|Причина, по которой отмена завершилась неудачно.

### <a name="example"></a>Пример

<!-- csl -->
```
.cancel operation c4b29441-4873-4e36-8310-c631c35c916e
```

|Операции|Операция|стартедон|канцеллатионстате|ReasonPhrase|
|---|---|---|---|---|
|c4b29441-4873-4e36-8310-c631c35c916e|материализедвиевкреатеоралтер|2020-05-08 19:45:03.9184142|Успешно отменено||

Если отмена не была завершена в течение 10 минут, `CancellationState` будет означать сбой. Создание может быть прервано.
