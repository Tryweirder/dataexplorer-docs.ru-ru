---
title: . Замена экстентов — Azure обозреватель данных
description: В этой статье описывается команда replace экстентов в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 07/02/2020
ms.openlocfilehash: cd193cb370136fd7f14a8892f157a895a1d7ad50
ms.sourcegitcommit: d6f35df833d5b4f2829a8924fffac1d0b49ce1c2
ms.contentlocale: ru-RU
ms.lasthandoff: 07/07/2020
ms.locfileid: "86060770"
---
# <a name="replace-extents"></a>. Замена экстентов

Эта команда выполняется в контексте определенной базы данных.
Он перемещает указанные экстенты из исходных таблиц в целевую таблицу, а затем удаляет указанные экстенты из целевой таблицы.
Все операции DROP и Move выполняются в одной транзакции.

Требуется [разрешение администратора таблицы](../management/access-control/role-based-authorization.md) для исходной и целевой таблиц.

> [!NOTE]
> Сегменты данных называются **экстентами** в Kusto, а все команды используют "экстент" или "экстенты" в качестве синонима.
> Дополнительные сведения о экстентах см. в статье [Общие сведения о экстентах (сегментах данных)](extents-overview.md).

## <a name="syntax"></a>Синтаксис

`.replace`[ `async` ] `extents` `in` `table` *DestinationTableName* `<| 
{` *запрос для удаляемых экстентов из* `},{` *запроса таблицы для перемещаемых экстентов в таблицу*`}`

* `async`(необязательно): асинхронное выполнение команды.
    * Возвращается идентификатор операции (GUID).
    * Состояние операции можно отслеживать. Используйте команду [. показ операций](operations.md#show-operations) .
    * Результаты успешного выполнения можно получить. Используйте команду [. отобразить сведения о операции](operations.md#show-operation-details) .

Чтобы указать, какие экстенты следует удалить или переместить, используйте один из двух запросов.
* *запрос к экстентам*, удаляемым из таблицы: результаты этого запроса указывают идентификаторы экстентов, которые должны быть удалены из целевой таблицы.
* *запрос к экстентам, перемещаемым в таблицу*: результаты этого запроса указывают идентификаторы экстентов в исходных таблицах, которые должны быть перемещены в целевую таблицу.

Оба запроса должны возвращать набор записей со столбцом с именем «Екстентид».

## <a name="restrictions"></a>Ограничения

* Исходная и целевая таблицы должны находиться в базе данных контекста.
* Все экстенты, заданные *запросом для экстентов* , удаляемых из таблицы, должны принадлежать целевой таблице.
* Все столбцы в исходных таблицах должны существовать в целевой таблице с тем же именем и типом данных.

## <a name="return-output-for-sync-execution"></a>Возвращаемые выходные данные (для выполнения синхронизации)

Выходной параметр |Type |Описание:
---|---|---
оригиналекстентид |string |Уникальный идентификатор (GUID) для исходного экстента в исходной таблице, перемещенный в целевую таблицу, или экстент в целевой таблице, который был удален.
ресултекстентид |string |Уникальный идентификатор (GUID) экстента результатов, который был перемещен из исходной таблицы в целевую таблицу. Пусто, если экстент был удален из целевой таблицы. При сбое: "Failed".
Сведения |string |Включает сведения об ошибке в случае сбоя операции.

> [!NOTE]
> Команда завершится ошибкой, если экстенты, возвращенные *экстентами для удаления из запроса таблицы* , не существуют в целевой таблице. Это может произойти, если экстенты были объединены до выполнения команды replace.
> Чтобы команда завершилась с ошибками в отсутствующих экстентах, убедитесь, что запрос возвращает ожидаемый Екстентидс. Пример #1 ниже завершится ошибкой, если экстент, который нужно удалить, не существует в таблице *мйосертабле*. В примере #2, однако, будет выполняться, даже если экстент, который нужно удалить, не существует, так как запрос для удаления не вернул идентификаторы экстентов.

## <a name="examples"></a>Примеры

### <a name="move-all-extents-from-two-tables"></a>Переместить все экстенты из двух таблиц 

Переместите все экстенты из двух конкретных таблиц ( `MyTable1` , `MyTable2` ) в таблицу `MyOtherTable` и отпустите все экстенты, `MyOtherTable` помеченные `drop-by:MyTag` :

```kusto
.replace extents in table MyOtherTable <|
    {
        .show table MyOtherTable extents where tags has 'drop-by:MyTag'
    },
    {
        .show tables (MyTable1,MyTable2) extents
    }
```

#### <a name="sample-output"></a>Пример выходных данных

|оригиналекстентид |ресултекстентид |Сведения
|---|---|---
|e133f050-a1e2-4dad-8552-1f5cf47cab69 |0d96ab2d-9dd2-4d2c-a45e-b24c65aa6687| 
|cdbeb35b-87ea-499f-b545-defbae091b57 |a90a303c-8a14-4207-8f35-d8ea94ca45be| 
|4fcb4598-9a31-4614-903c-0c67c286da8c |97aafea1-59ff-4312-B06B-08f42187872f| 
|2dfdef64-62a3-4950-a130-96b5b1083b5a |0fb7f3da-5e28-4f09-a000-e62eb41592df| 

### <a name="move-all-extents-from-one-table-to-another-drop-specific-extent"></a>Переместить все экстенты из одной таблицы в другую, удалить определенную область

Переместить все экстенты из одного конкретного тавле ( `MyTable1` ) в таблицу `MyOtherTable` и удалить определенную область в `MyOtherTable` , по ее идентификатору:

```kusto
.replace extents in table MyOtherTable <|
    {
        print ExtentId = "2cca5844-8f0d-454e-bdad-299e978be5df"
    },
    {
        .show table MyTable1 extents 
    }
```

```kusto
.replace extents in table MyOtherTable  <|
    {
        .show table MyOtherTable extents
        | where ExtentId == guid(2cca5844-8f0d-454e-bdad-299e978be5df) 
    },
    {
        .show table MyTable1 extents 
    }
```

### <a name="implement-an-idempotent-logic"></a>Реализация логики идемпотентными

Реализуйте логику идемпотентными, чтобы Kusto удаляет экстенты из таблицы, `t_dest` только если есть экстенты для перемещения из таблицы `t_source` в таблицу `t_dest` :

```kusto
.replace async extents in table t_dest <|
{
    let any_extents_to_move = toscalar( 
        t_source
        | where extent_tags() has 'drop-by:blue'
        | summarize count() > 0
    );
    let extents_to_drop =
        t_dest
        | where any_extents_to_move and extent_tags() has 'drop-by:blue'
        | summarize by ExtentId = extent_id()
    ;
    extents_to_drop
},
{
    let extents_to_move = 
        t_source
        | where extent_tags() has 'drop-by:blue'
        | summarize by ExtentId = extent_id()
    ;
    extents_to_move
}
```
