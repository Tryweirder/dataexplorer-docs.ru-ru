---
title: обобщенный оператор - Azure Data Explorer Документы Майкрософт
description: В этой статье описывается обобщенный оператор в Azure Data Explorer.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 03/20/2020
ms.openlocfilehash: ed1808f173d0f779c84f9405987d7395de833120
ms.sourcegitcommit: 436cd515ea0d83d46e3ac6328670ee78b64ccb05
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 04/21/2020
ms.locfileid: "81663224"
---
# <a name="summarize-operator"></a>Оператор summarize

Создает таблицу, которая объединяет содержимое входной таблицы.

```kusto
T | summarize count(), avg(price) by fruit, supplier
```

Таблица, которая показывает количество и среднюю цену каждого фрукта от каждого поставщика. Там в строке в выходе для каждого отдельного сочетания фруктов и поставщиков. В выходных столбцах показаны количество, средняя цена, фрукты и поставщик. Все остальные входные столбцы игнорируются.

```kusto
T | summarize count() by price_range=bin(price, 10.0)
```

В таблице отображено количество элементов с ценами в каждом интервале [0,10.0], [10.0,20.0] и т. д. Пример включает один столбец для подсчета и один столбец для диапазон цен. Все остальные входные столбцы игнорируются.

**Синтаксис**

*T* `| summarize` -*Колонка* `=`-`,` *Агрегация* . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . `by` Колонка*Column* `=`- *Групповое выражение* .`,` . . . . . . . . . . . . . . . . . . . . . . . . .

**Аргументы**

* *Column* — необязательное имя итогового столбца. По умолчанию это имя, получаемое из выражения.
* *Агрегация:* Вызов [функции агрегации,](summarizeoperator.md#list-of-aggregation-functions) `count()` такой `avg()`как или с именами столбцов в качестве аргументов. Смотрите [список функций агрегирования.](summarizeoperator.md#list-of-aggregation-functions)
* *GroupExpression* — выражение для столбцов, предоставляющее набор уникальных значений. Обычно это имя столбца, который уже содержит ограниченный набор значений, либо функция `bin()` с числовым или временным столбцом в качестве аргумента. 

> [!NOTE]
> Когда таблица ввода пуста, вывод зависит от того, используется ли *GroupExpression:*
>
> * Если *GroupExpression* не предоставляется, выход будет одним (пустым) строкой.
> * Если *GroupExpression* предоставляется, выход не будет иметь строк.

**Возвращает**

Входные строки объединяются в группы с одинаковыми значениями выражений `by` . Затем указанные агрегатные функции выполняют вычисления и создают строку для каждой группы. Результат содержит столбцы `by` и хотя бы один столбец для каждого вычисленного статистического выражения. (Некоторые агрегатные функции возвращают несколько столбцов).

В результате есть столько строк, сколько `by` и отдельных комбинаций значений (которые могут быть равны нулю). Если не предусмотрены групповые ключи, результат имеет одну запись.

Чтобы суммировать диапазоны числовых `bin()` значений, используйте для уменьшения диапазонов до дискретных значений.

> [!NOTE]
> * Для агрегатных выражений и выражений группирования допускаются произвольные выражения, но эффективнее использовать простые имена столбцов или функцию `bin()` для числовых столбцов.
> * Автоматические почасовые ячейки для столбцов времени даты больше не поддерживаются. Вместо этого используйте явные биннингы. Например, `summarize by bin(timestamp, 1h)`.

## <a name="list-of-aggregation-functions"></a>Список функций агрегирования

|Компонент|Описание|
|--------|-----------|
|[любой()](any-aggfunction.md)|Возвращает случайное непустое значение для группы|
|[anyif()](anyif-aggfunction.md)|Возвращает случайное непустое значение для группы (с предикатом)|
|[arg_max()](arg-max-aggfunction.md)|Возвращает одно или несколько выражений, когда аргумент максимизируется|
|[arg_min()](arg-min-aggfunction.md)|Возвращает одно или несколько выражений, когда аргумент сведен к минимуму|
|[avg()](avg-aggfunction.md)|Возвращает среднее значение по всей группе|
|[avgif()](avgif-aggfunction.md)|Возвращает среднее значение по всей группе (с предикатом)|
|[binary_all_and](binary-all-and-aggfunction.md)|Возвращает агрегированное `AND` значение с помощью двоичного файла группы|
|[binary_all_or](binary-all-or-aggfunction.md)|Возвращает агрегированное `OR` значение с помощью двоичного файла группы|
|[binary_all_xor](binary-all-xor-aggfunction.md)|Возвращает агрегированное `XOR` значение с помощью двоичного файла группы|
|[buildschema()](buildschema-aggfunction.md)|Возвращает минимальную схему, `dynamic` которая допускает все значения ввода|
|[количество ()](count-aggfunction.md)|Возвращает количество группы|
|[countif()](countif-aggfunction.md)|Возвращает счет с предикатом группы|
|[всчет()](dcount-aggfunction.md)|Возвращает приблизительное количество элементов группы|
|[dcountif()](dcountif-aggfunction.md)|Возвращает приблизительное количество элементов группы (с предикатом)|
|[make_bag()](make-bag-aggfunction.md)|Возвращает пакет свойств динамических значений внутри группы|
|[make_bag_if()](make-bag-if-aggfunction.md)|Возвращает пакет свойств динамических значений внутри группы (с предикатом)|
|[make_list()](makelist-aggfunction.md)|Возвращает список всех значений в группе|
|[make_list_if()](makelistif-aggfunction.md)|Возвращает список всех значений в группе (с предикатом)|
|[make_list_with_nulls()](make-list-with-nulls-aggfunction.md)|Возвращает список всех значений в группе, включая нулевые значения|
|[make_set()](makeset-aggfunction.md)|Возвращает набор различных значений внутри группы|
|[make_set_if()](makesetif-aggfunction.md)|Возвращает набор различных значений внутри группы (с предикатом)|
|[макс()](max-aggfunction.md)|Возвращает максимальное значение по всей группе|
|[maxif()](maxif-aggfunction.md)|Возвращает максимальное значение по всей группе (с предикатом)|
|[мин()](min-aggfunction.md)|Возвращает минимальное значение по всей группе|
|[minif()](minif-aggfunction.md)|Возвращает минимальное значение по всей группе (с предикатом)|
|[процентиол()](percentiles-aggfunction.md)|Возвращает процентиль приблизительный группы|
|[percentiles_array()](percentiles-aggfunction.md)|Возвращает проценти приблизительные группы|
|[percentilesw()](percentiles-aggfunction.md)|Возвращает взвешенный процентиль, приблизительный группы|
|[percentilesw_array()](percentiles-aggfunction.md)|Возвращает взвешенные проценти, приблизительные группы|
|[stdev()](stdev-aggfunction.md)|Возвращает стандартное отклонение по всей группе|
|[stdevif()](stdevif-aggfunction.md)|Возвращает стандартное отклонение по всей группе (с предикатом)|
|[сумма()](sum-aggfunction.md)|Возвращает сумму элементов с группой|
|[sumif()](sumif-aggfunction.md)|Возвращает сумму элементов с группой (с предикатом)|
|[дисперсия()](variance-aggfunction.md)|Возвращает дисперсию по всей группе|
|[varianceif()](varianceif-aggfunction.md)|Возвращает дисперсию по всей группе (с предикатом)|

## <a name="aggregates-default-values"></a>Значения по умолчанию

В следующей таблице кратко излагаются значения агрегаций по умолчанию:

Оператор       |Значение по умолчанию                         
---------------|------------------------------------
 `count()`, `countif()`, `dcount()`, `dcountif()`         |   0                            
 `make_bag()`, `make_bag_if()`, `make_list()`, `make_list_if()`, `make_set()`, `make_set_if()` |    пустой динамический массив (яп.          
 Все остальные          |   null                           

 При использовании этих агрегатов по объектам, включающим нулевые значения, значения null будут проигнорированы и не будут участвовать в расчете (см. примеры ниже).

## <a name="examples"></a>Примеры

![альт текст](./Images/aggregations/01.png "01")

**Пример**

Определите, какие `ActivityType` уникальные комбинации и `CompletionStatus` есть в таблице. Функции агрегирования не существуют, только клавиши группы. Выход будет просто показать столбцы для этих результатов:

```kusto
Activities | summarize by ActivityType, completionStatus
```

|`ActivityType`|`completionStatus`
|---|---
|`dancing`|`started`
|`singing`|`started`
|`dancing`|`abandoned`
|`singing`|`completed`

**Пример**

Находит минимальную и максимальную отметку времени всех записей в таблице «Действия». Предложение group by отсутствует, поэтому в выходных данных всего одна строка:

```kusto
Activities | summarize Min = min(Timestamp), Max = max(Timestamp)
```

|`Min`|`Max`
|---|---
|`1975-06-09 09:21:45` | `2015-12-24 23:45:00`

**Пример**

Создайте строку для каждого континента, показывая количество городов, в которых происходят мероприятия. Поскольку значений для "континента" мало, в пункте "по" не требуется функция группировки:

    Activities | summarize cities=dcount(city) by continent

|`cities`|`continent`
|---:|---
|`4290`|`Asia`|
|`3267`|`Europe`|
|`2673`|`North America`|


**Пример**

Следующий пример вычисляет гистограмму для каждого типа активности. Поскольку `Duration` имеет много `bin` значений, используйте для группы его значений в 10-минутные интервалы:

```kusto
Activities | summarize count() by ActivityType, length=bin(Duration, 10m)
```

|`count_`|`ActivityType`|`length`
|---:|---|---
|`354`| `dancing` | `0:00:00.000`
|`23`|`singing` | `0:00:00.000`
|`2717`|`dancing`|`0:10:00.000`
|`341`|`singing`|`0:10:00.000`
|`725`|`dancing`|`0:20:00.000`
|`2876`|`singing`|`0:20:00.000`
|...

**Пример значений по умолчанию агрегатов**

Когда вводной `summarize` связи оператора имеет по крайней мере один пустой ключ группы, его результат тоже пуст.

Когда ввод `summarize` оператора не имеет пустого ключа группы, результатом являются значения по умолчанию `summarize`агрегатов, используемых в:

```kusto
range x from 1 to 10 step 1
| where 1 == 2
| summarize any(x), arg_max(x, x), arg_min(x, x), avg(x), buildschema(todynamic(tostring(x))), max(x), min(x), percentile(x, 55), hll(x) ,stdev(x), sum(x), sumif(x, x > 0), tdigest(x), variance(x)
```

|any_x|max_x|max_x_x|min_x|min_x_x|avg_x|schema_x|max_x1|min_x1|percentile_x_55|hll_x|stdev_x|sum_x|sumif_x|tdigest_x|variance_x|
|---|---|---|---|---|---|---|---|---|---|---|---|---|---|---|---|
|||||||||||||||||

```kusto
range x from 1 to 10 step 1
| where 1 == 2
| summarize  count(x), countif(x > 0) , dcount(x), dcountif(x, x > 0)
```

|count_x|countif_|dcount_x|dcountif_x|
|---|---|---|---|
|0|0|0|0|

```kusto
range x from 1 to 10 step 1
| where 1 == 2
| summarize  make_set(x), make_list(x)
```

|set_x|list_x|
|---|---|
|[]|[]|

Совокупный avg суммирует все не-нули и учитывает только те, которые участвовали в расчете (не будет принимать в расчет нулевки).

```kusto
range x from 1 to 2 step 1
| extend y = iff(x == 1, real(null), real(5))
| summarize sum(y), avg(y)
```

|sum_y|avg_y|
|---|---|
|5|5|

Регулярный подсчет будет считатьнул: 

```kusto
range x from 1 to 2 step 1
| extend y = iff(x == 1, real(null), real(5))
| summarize count(y)
```

|count_y|
|---|
|2|

```kusto
range x from 1 to 2 step 1
| extend y = iff(x == 1, real(null), real(5))
| summarize make_set(y), make_set(y)
```

|set_y|set_y1|
|---|---|
|[5.0]|[5.0]|
