---
title: подключаемый модуль автокластера — Azure обозреватель данных
description: В этой статье описывается подключаемый модуль автокластеров в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 02/13/2020
ms.openlocfilehash: 0b2d76c0dd7a3ee0724db67aa8cb0cd9229748fa
ms.sourcegitcommit: 39b04c97e9ff43052cdeb7be7422072d2b21725e
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 05/12/2020
ms.locfileid: "83225536"
---
# <a name="autocluster-plugin"></a>подключаемый модуль автокластера

```kusto
T | evaluate autocluster()
```

AutoCluster находит распространенные шаблоны дискретных атрибутов (измерения) в данных и сокращает результаты исходного запроса (будь то 100 или 100 тысяч строк) до небольшого числа шаблонов. Эта функция предназначена для анализа сбоев (например, исключений, поломок), но потенциально может работать с любым отфильтрованным набором данных. 

**Синтаксис**

`T | evaluate autocluster(`*аргументы*`)`

**Возвращает**

AutoCluster возвращает набор шаблонов (как правило, небольшого размера), которые содержат фрагменты данных с одинаковыми значениями для нескольких дискретных атрибутов. В результатах каждый шаблон соответствует строке. 

Первый столбец — это идентификатор сегмента. Следующие два столбца — это количество и процент строк из исходного запроса, захваченных шаблоном. Остальные столбцы берутся из исходного запроса. Их значения соответствуют конкретным значениям в столбцах или подстановочным значениям (которыми по умолчанию являются значения NULL), представляющим значения переменных. 

Обратите внимание, что шаблоны не являются уникальными. Они могут перекрываться и обычно не охватывают все исходные строки. Некоторые строки могут не охватываться ни одним из шаблонов.

> [!TIP]
> Используйте [WHERE](./whereoperator.md) и [Project](./projectoperator.md) во входном канале, чтобы уменьшить объем данных до того, что вас интересует.
>
> Если вы хотите узнать дополнительные сведения о конкретной строке, добавьте ее значения в фильтр `where` .

**Аргументы (необязательные)**

`T | evaluate autocluster(`[*SizeWeight*, *WeightColumn*, *NumSeeds*, *CustomWildcard*, *CustomWildcard*,...]`)`

Все аргументы необязательны, но они должны быть упорядочены, как показано выше. Чтобы указать, что следует использовать значение по умолчанию, добавьте знак тильды в строку "~" (см. примеры ниже).

Доступные аргументы:

* SizeWeight-0 < *double* < 1 [по умолчанию: 0,5]

    Позволяет управлять балансом между универсальностью (большой охват) и информативностью (много общих значений). При увеличении значения количество шаблонов, как правило, уменьшается, а каждый шаблон охватывает больший процент данных. Если уменьшить значение, обычно получаются более конкретные шаблоны с большим количеством общих значений и меньшим процентом охвата. Под формулой с неинформативностью понимается взвешенное среднее геометрическое значение между нормализованным универсальным показателем, а `SizeWeight` `1-SizeWeight` также в качестве весовых коэффициентов. 

    Пример: `T | evaluate autocluster(0.8)`

* WeightColumn — *column_name*

    Рассматривает каждую строку входных данных в соответствии с указанным весовым коэффициентом (по умолчанию весовой коэффициент каждой строки — 1). Аргумент должен быть именем числового столбца (например, int, long, real). Как правило, при использовании столбца с весовым коэффициентом следует учитывать выборку, группирование или агрегирование данных, внедренных в каждой строке.
    
    Пример: `T | evaluate autocluster('~', sample_Count)` 

* NumSeeds- *int* [по умолчанию: 25] 

    Число начальных значений определяет число начальных локальных точек поиска для алгоритма. В некоторых случаях в зависимости от структуры данных при увеличении числа начальных значений увеличивается число (или повышается качество) результатов. Это достигается за счет увеличения области поиска при меньшей скорости выполнения запросов. Это значение уменьшает результаты в обоих направлениях, поэтому уменьшение его ниже 5 приведет к незначительному повышению производительности и увеличению свыше 50 редко создаст дополнительные шаблоны.

    Пример: `T | evaluate autocluster('~', '~', 15)`

* CustomWildcard — *"any_value_per_type"*

    Задает подстановочный знак для определенного типа в таблице результатов, который укажет, что текущий шаблон не имеет ограничения по этому столбцу.
    По умолчанию имеет значение NULL, для строки по умолчанию указывается пустая строка. Если значение по умолчанию является допустимым значением в данных, следует использовать другое подстановочное значение (например, `*` ).

    Пример: `T | evaluate autocluster('~', '~', '~', '*', int(-1), double(-1), long(0), datetime(1900-1-1))`

**Пример**

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
StormEvents 
| where monthofyear(StartTime) == 5
| extend Damage = iff(DamageCrops + DamageProperty > 0 , "YES" , "NO")
| project State , EventType , Damage
| evaluate autocluster(0.6)
```

|Идентификатор сегмента|Счетчик|Процент|State|EventType|Ущерб|
|---|---|---|---|---|---|---|---|---|
|0|2278|38,7||Град|NO
|1|512|8,7||Шквалистый ветер|YES
|2|898|15,3|Техас||

**Примеры с пользовательскими подстановочными знаками**

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
StormEvents 
| where monthofyear(StartTime) == 5
| extend Damage = iff(DamageCrops + DamageProperty > 0 , "YES" , "NO")
| project State , EventType , Damage 
| evaluate autocluster(0.2, '~', '~', '*')
```

|Идентификатор сегмента|Счетчик|Процент|State|EventType|Ущерб|
|---|---|---|---|---|---|---|---|---|
|0|2278|38,7|\*|Град|NO
|1|512|8,7|\*|Шквалистый ветер|YES
|2|898|15,3|Техас|\*|\*

**См. также:**

* [basket](./basketplugin.md)
* [свести](./reduceoperator.md)

**Дополнительные сведения**

* Функция AutoCluster преимущественно основана на алгоритме Seed-Expand, с которым вы можете ознакомиться в документе, посвященном [алгоритмам для интеллектуального анализа данных телеметрии с помощью дискретных атрибутов](https://www.scitepress.org/DigitalLibrary/PublicationsDetail.aspx?ID=d5kcrO+cpEU=&t=1). 

