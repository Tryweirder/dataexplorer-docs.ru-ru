---
title: series_fir () — обозреватель данных Azure
description: В этой статье описывается series_fir () в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: alexans
ms.service: data-explorer
ms.topic: reference
ms.date: 02/19/2019
ms.openlocfilehash: 8085455616fc97337ca115c1ef5b0c0e2422e08e
ms.sourcegitcommit: 608539af6ab511aa11d82c17b782641340fc8974
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 10/20/2020
ms.locfileid: "92246182"
---
# <a name="series_fir"></a>series_fir()

Применяет фильтр конечного ответа (FIR) к ряду.  

Функция принимает выражение, содержащее динамический числовой массив, в качестве входных данных и применяет фильтр [откликов с ограничением](https://en.wikipedia.org/wiki/Finite_impulse_response) на недопустимый. Указав `filter` коэффициенты, можно использовать для вычисления скользящего среднего, смягчения, обнаружения изменений и многих других вариантов использования. Функция принимает столбец, содержащий динамический массив, и статический динамический массив коэффициентов фильтра в качестве входных данных и применяет фильтр к столбцу. Она выводит новый столбец динамического массива, содержащий отфильтрованные результаты.  

## <a name="syntax"></a>Синтаксис

`series_fir(`*x* `,` *Фильтр* [ `,` *нормализация*[ `,` *центр*]]`)`

## <a name="arguments"></a>Аргументы

* *x*: ячейка динамического массива числовых значений. Обычно результат выполнения операторов [make-Series](make-seriesoperator.md) или [make_list](makelist-aggfunction.md) .
* *Filter*: константное выражение, содержащее коэффициенты фильтра (хранимые в виде динамического массива числовых значений).
* *нормализация*: Необязательное логическое значение, указывающее, следует ли нормализовать фильтр. То есть, делится на сумму коэффициентов. Если фильтр содержит отрицательные значения, то необходимо указать *нормализацию* как `false` , в противном случае результатом будет `null` . Если значение не указано, предполагается использование значения по умолчанию *нормализации* в зависимости от наличия отрицательных значений в *фильтре*. Если *Фильтр* содержит хотя бы одно отрицательное значение, предполагается *нормализация* `false` .  
Нормализация — это удобный способ убедиться, что сумма коэффициентов равна 1. Затем фильтр не повысить эффективность или не приводит к ослаблению последовательности. Например, скользящее среднее из четырех ячеек может быть задано *фильтром*= [1, 1, 1, 1] и *нормализованным*= true, что проще, чем ввод [0,25, 0.25.0.25, 0,25].
* по *центру*: Необязательное логическое значение, указывающее, применяется ли фильтр к временному окну до и после текущей точки, а также в интервале времени, начиная с текущего момента, в обратном направлении. По умолчанию центр имеет значение false, что соответствует сценарию потоковой передачи данных, где можно применить фильтр только к текущим и более старым точкам. Однако для нерегламентированной обработки можно задать значение `true` , чтобы синхронизировать его с временными рядами. Примеры см. ниже. Этот параметр управляет [задержкой группы](https://en.wikipedia.org/wiki/Group_delay_and_phase_delay)фильтра.

## <a name="examples"></a>Примеры

* Вычислите скользящее среднее из пяти точек, установив *Filter*= [1, 1, 1, 1, 1] и *нормализацию* = `true` (по умолчанию). Обратите внимание на результат действия *Center* = `false` (по умолчанию) и `true` :

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
range t from bin(now(), 1h)-23h to bin(now(), 1h) step 1h
| summarize t=make_list(t)
| project id='TS', val=dynamic([0,0,0,0,0,0,0,0,0,10,20,40,100,40,20,10,0,0,0,0,0,0,0,0]), t
| extend 5h_MovingAvg=series_fir(val, dynamic([1,1,1,1,1])),
         5h_MovingAvg_centered=series_fir(val, dynamic([1,1,1,1,1]), true, true)
| render timechart
```

Этот запрос возвращает:  
*5h_MovingAvg*: фильтр скользящего среднего по пяти точкам. Пик сглажен и его вершина смещена на (5 – 1)/2 = 2 ч.  
*5h_MovingAvg_centered*: то же, что и параметр `center=true` , пиковое расположение остается в исходном расположении.

:::image type="content" source="images/series-firfunction/series-fir.png" alt-text="Ряд FIR" border="false":::

* Чтобы вычислить разницу между точкой и ее предшествующей, задайте *Filter*= [1,-1].

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
range t from bin(now(), 1h)-11h to bin(now(), 1h) step 1h
| summarize t=make_list(t)
| project id='TS',t,value=dynamic([0,0,0,0,2,2,2,2,3,3,3,3])
| extend diff=series_fir(value, dynamic([1,-1]), false, false)
| render timechart
```

:::image type="content" source="images/series-firfunction/series-fir2.png" alt-text="Ряд FIR" border="false":::
