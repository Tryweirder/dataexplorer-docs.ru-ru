---
title: series_fir () — обозреватель данных Azure
description: В этой статье описывается series_fir () в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 02/19/2019
ms.openlocfilehash: 0cb1f2b414c22220f8cdc81475c5cb0a3d9aedcf
ms.sourcegitcommit: bb8c61dea193fbbf9ffe37dd200fa36e428aff8c
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 05/13/2020
ms.locfileid: "83372752"
---
# <a name="series_fir"></a>series_fir()

Применяет к ряду фильтр конечных ответов с ограничением по некоторому импульсу.  

Принимает выражение, содержащее динамический числовой массив, в качестве входных данных и применяет фильтр [откликов с конечным импульсом](https://en.wikipedia.org/wiki/Finite_impulse_response) . Указав `filter` коэффициенты, можно использовать для вычисления скользящего среднего, смягчения, обнаружения изменений и многих других вариантов использования. Функция принимает входной столбец, содержащий динамический массив, и статический массив коэффициентов фильтра и применяет фильтр к столбцу. Она выводит новый столбец динамического массива, содержащий отфильтрованные результаты.  

**Синтаксис**

`series_fir(`*x* `,` *Фильтр* [ `,` *нормализация*[ `,` *центр*]]`)`

**Аргументы**

* *x*: ячейка динамического массива, которая представляет собой массив числовых значений, обычно результирующий результат операторов [make-Series](make-seriesoperator.md) или [make_list](makelist-aggfunction.md) .
* *Filter*: константное выражение, содержащее коэффициенты фильтра (хранимые в виде динамического массива числовых значений).
* *нормализация*: Необязательное логическое значение, указывающее, следует ли нормализовать фильтр (то есть разделить на сумму коэффициентов). Если *Фильтр* содержит отрицательные значения, то необходимо указать *нормализовать* как `false` , иначе результат будет `null` . Если этот параметр не указан, предполагается, что в зависимости от наличия отрицательных значений в *фильтре*используется значение *нормализации* по умолчанию: Если *Фильтр* содержит по крайней мере одно отрицательное значение, предполагается использование *нормализации* `false` .  
Нормализация — это удобный способ гарантировать, что сумма коэффициентов равна 1, поэтому фильтр не будет расширять или приводить к ослаблению цикла. Например, скользящее среднее из 4 ячеек может быть задано *фильтром*= [1, 1, 1, 1] и *нормализованным*= true, что проще, чем ввод [0,25, 0.25.0.25, 0,25].
* по *центру*: Необязательное логическое значение, указывающее, применяется ли фильтр к временному окну до и после текущей точки, а также в интервале времени, начиная с текущего момента, в обратном направлении. По умолчанию center имеет значение false, которое подходит для потоковой передачи данных, где фильтр можно применить только к текущим и более ранним точкам. Тем не менее для ad-hoc-обработки можно задать значение true, обеспечив синхронизацию с временными рядами (см. примеры ниже). Технически говоря, этот параметр управляет [задержкой группы](https://en.wikipedia.org/wiki/Group_delay_and_phase_delay)фильтра.

**Примеры**

* Вычисление скользящего среднего по 5 точкам может быть выполнено путем установки *фильтра*= [1, 1, 1, 1, 1] и *нормализации* = `true` (по умолчанию). Обратите внимание на результат действия *Center* = `false` (по умолчанию) и `true` :

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
range t from bin(now(), 1h)-23h to bin(now(), 1h) step 1h
| summarize t=make_list(t)
| project id='TS', val=dynamic([0,0,0,0,0,0,0,0,0,10,20,40,100,40,20,10,0,0,0,0,0,0,0,0]), t
| extend 5h_MovingAvg=series_fir(val, dynamic([1,1,1,1,1])),
         5h_MovingAvg_centered=series_fir(val, dynamic([1,1,1,1,1]), true, true)
| render timechart
```

Этот запрос возвращает:  
*5h_MovingAvg*: фильтр скользящего среднего по 5 точкам. Пик сглажен и его вершина смещена на (5 – 1)/2 = 2 ч.  
*5h_MovingAvg_centered*. то же, но с параметром Center = true приводит к тому, что пик остается в исходном расположении.

:::image type="content" source="images/series-firfunction/series-fir.png" alt-text="Ряд FIR" border="false":::

* Вычисление разницы между точкой и ее предшествующей можно выполнить с помощью параметра *Filter*= [1,-1]:

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
range t from bin(now(), 1h)-11h to bin(now(), 1h) step 1h
| summarize t=make_list(t)
| project id='TS',t,value=dynamic([0,0,0,0,2,2,2,2,3,3,3,3])
| extend diff=series_fir(value, dynamic([1,-1]), false, false)
| render timechart
```

:::image type="content" source="images/series-firfunction/series-fir2.png" alt-text="Ряд FIR 2" border="false":::
