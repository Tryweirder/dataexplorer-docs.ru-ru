---
title: подключаемый модуль activity_metrics — Azure обозреватель данных | Документация Майкрософт
description: В этой статье описывается подключаемый модуль activity_metrics в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 02/13/2020
ms.openlocfilehash: 2ab8a9b8a687b695859c52e75ab4e9f88aac408b
ms.sourcegitcommit: 05489ce5257c0052aee214a31562578b0ff403e7
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 08/25/2020
ms.locfileid: "88793699"
---
# <a name="activity_metrics-plugin"></a>Подключаемый модуль activity_metrics

Вычисляет полезные метрики действий (значения числа различных объектов, количество различных новых значений, скорость хранения и скорость обработки) на основе окна текущий период в сравнении с предыдущим периодом (в отличие от [подключаемого модуля activity_counts_metrics](activity-counts-metrics-plugin.md) , в котором каждое окно сравнивается со *всеми* предыдущими окнами времени).

```kusto
T | evaluate activity_metrics(id, datetime_column, startofday(ago(30d)), startofday(now()), 1d, dim1, dim2, dim3)
```

## <a name="syntax"></a>Синтаксис

*T* `| evaluate` `activity_metrics(` *идколумн* `,` *тимелинеколумн* `,` [*начальный* `,` *конец* `,` ] *Window* [ `,` *Dim1* `,` *dim2* `,` ...]`)`

## <a name="arguments"></a>Аргументы

* *T*: Входное табличное выражение.
* *Идколумн*: имя столбца со значениями идентификаторов, представляющими действия пользователя. 
* *Тимелинеколумн*: имя столбца, представляющего временную шкалу.
* *Start*(необязательно) — скаляр со значением начального периода анализа.
* *End*(необязательный) — скаляр со значением периода окончания анализа.
* *Window*: скалярный со значением периода окна анализа. Может быть либо числовым, либо значением DateTime или timestamp, либо строкой, которая является одним из `week` / `month` / `year` , в этом случае все периоды будут [startofweek](startofweekfunction.md) / [StartOfMonth](startofmonthfunction.md) / [startofyear](startofyearfunction.md) соответствующим образом. 
* *Dim1*, *dim2*,...: (необязательно) список столбцов измерений, которые срезируют вычисление метрик действия.

## <a name="returns"></a>Возвращаемое значение

Возвращает таблицу с различными значениями числа различных значений, скоростью хранения и частотой обновления для каждого периода временной шкалы и для каждого сочетания существующих измерений.

Схема выходной таблицы:

|*тимелинеколумн*|dcount_values|dcount_newvalues|retention_rate|churn_rate|Dim1|..|dim_n|
|---|---|---|---|---|--|--|--|--|--|--|
|Тип: от *тимелинеколумн*|long|long|double|double|..|..|..|

**Примечания**

***Определение частоты удержания***

`Retention Rate` за период вычисляется следующим образом:

> *количество клиентов, возвращенных за период*  
> /(деление на)  
> *число клиентов в начале периода*  

где `# of customers returned during the period` определяется как:

> *число клиентов в конце периода*  
> \- знак  
> *число новых клиентов, приобретенных за период*  

`Retention Rate` может варьироваться от 0,0 до 1,0  
Более высокая оценка означает больший объем возвращаемых пользователей.


***Определение частоты обновлений***

`Churn Rate` за период вычисляется следующим образом:
    
> *число клиентов, потерянных за период*  
> /(деление на)  
> *число клиентов в начале периода*  

где `# of customer lost in the period` определяется как:

> *число клиентов в начале периода*  
> \- знак  
> *число клиентов в конце периода*  

`Churn Rate` может варьироваться от 0,0 до 1,0. более высокая оценка означает, что большее количество пользователей не возвращается в службу.

***Сравнение обновлений и скорости хранения***

Производным от определения `Churn Rate` и `Retention Rate` , следующим всегда является true:

> [ `Retention Rate` ] = 100,0%-[ `Churn Rate` ]


## <a name="examples"></a>Примеры

### <a name="weekly-retention-rate-and-churn-rate"></a>Еженедельный темп хранения и частота обновлений

Следующий запрос вычисляет скорость хранения и обновления для недельного окна недели.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
// Generate random data of user activities
let _start = datetime(2017-01-02);
let _end = datetime(2017-05-31);
range _day from _start to _end  step 1d
| extend d = tolong((_day - _start)/1d)
| extend r = rand()+1
| extend _users=range(tolong(d*50*r), tolong(d*50*r+200*r-1), 1) 
| mv-expand id=_users to typeof(long) limit 1000000
// 
| evaluate activity_metrics(['id'], _day, _start, _end, 7d)
| project _day, retention_rate, churn_rate
| render timechart 
```

|_day|retention_rate|churn_rate|
|---|---|---|
|2017-01-02 00:00:00.0000000|Не число|Не число|
|2017-01-09 00:00:00.0000000|0.179910044977511|0.820089955022489|
|2017-01-16 00:00:00.0000000|0.744374437443744|0.255625562556256|
|2017-01-23 00:00:00.0000000|0.612096774193548|0.387903225806452|
|2017-01-30 00:00:00.0000000|0.681141439205955|0.318858560794045|
|2017-02-06 00:00:00.0000000|0.278145695364238|0.721854304635762|
|2017-02-13 00:00:00.0000000|0.223172628304821|0.776827371695179|
|2017-02-20 00:00:00.0000000|0,38|0,62|
|2017-02-27 00:00:00.0000000|0.295519001701645|0.704480998298355|
|2017-03-06 00:00:00.0000000|0.280387770320656|0.719612229679344|
|2017-03-13 00:00:00.0000000|0.360628154795289|0.639371845204711|
|2017-03-20 00:00:00.0000000|0.288008028098344|0.711991971901656|
|2017-03-27 00:00:00.0000000|0.306134969325153|0.693865030674847|
|2017-04-03 00:00:00.0000000|0.356866537717602|0.643133462282398|
|2017-04-10 00:00:00.0000000|0.495098039215686|0.504901960784314|
|2017-04-17 00:00:00.0000000|0.198296836982968|0.801703163017032|
|2017-04-24 00:00:00.0000000|0.0618811881188119|0.938118811881188|
|2017-05-01 00:00:00.0000000|0.204657727593507|0.795342272406493|
|2017-05-08 00:00:00.0000000|0.517391304347826|0.482608695652174|
|2017-05-15 00:00:00.0000000|0.143667296786389|0.856332703213611|
|2017-05-22 00:00:00.0000000|0.199122325836533|0.800877674163467|
|2017-05-29 00:00:00.0000000|0.063468992248062|0.936531007751938|

:::image type="content" source="images/activity-metrics-plugin/activity-metrics-churn-and-retention.png" border="false" alt-text="Обработка и хранение метрик действий":::

### <a name="distinct-values-and-distinct-new-values"></a>Уникальные значения и уникальные значения "New" 

Следующий запрос вычисляет различные значения и новые значения (идентификаторы, которые не отображались в предыдущем временном окне) для окна неделя за неделю.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
// Generate random data of user activities
let _start = datetime(2017-01-02);
let _end = datetime(2017-05-31);
range _day from _start to _end  step 1d
| extend d = tolong((_day - _start)/1d)
| extend r = rand()+1
| extend _users=range(tolong(d*50*r), tolong(d*50*r+200*r-1), 1) 
| mv-expand id=_users to typeof(long) limit 1000000
// 
| evaluate activity_metrics(['id'], _day, _start, _end, 7d)
| project _day, dcount_values, dcount_newvalues
| render timechart 
```

|_day|dcount_values|dcount_newvalues|
|---|---|---|
|2017-01-02 00:00:00.0000000|630|630|
|2017-01-09 00:00:00.0000000|738|575|
|2017-01-16 00:00:00.0000000|1187|841|
|2017-01-23 00:00:00.0000000|1092|465|
|2017-01-30 00:00:00.0000000|1261|647|
|2017-02-06 00:00:00.0000000|1744|1043|
|2017-02-13 00:00:00.0000000|1563|432|
|2017-02-20 00:00:00.0000000|1406|818|
|2017-02-27 00:00:00.0000000|1956|1429|
|2017-03-06 00:00:00.0000000|1593|848|
|2017-03-13 00:00:00.0000000|1801|1423|
|2017-03-20 00:00:00.0000000|1710|1017|
|2017-03-27 00:00:00.0000000|1796|1516|
|2017-04-03 00:00:00.0000000|1381|1008|
|2017-04-10 00:00:00.0000000|1756|1162|
|2017-04-17 00:00:00.0000000|1831|1409|
|2017-04-24 00:00:00.0000000|1823|1164|
|2017-05-01 00:00:00.0000000|1811|1353|
|2017-05-08 00:00:00.0000000|1691|1246|
|2017-05-15 00:00:00.0000000|1812|1608|
|2017-05-22 00:00:00.0000000|1740|1017|
|2017-05-29 00:00:00.0000000|960|756|

:::image type="content" source="images/activity-metrics-plugin/activity-metrics-dcount-and-dcount-newvalues.png" border="false" alt-text="Метрики действия DCount и DCount новые значения":::
