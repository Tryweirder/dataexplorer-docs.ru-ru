---
title: Оператор MV-Expand — Azure обозреватель данных
description: В этой статье описывается оператор MV-Expand в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 02/24/2019
ms.openlocfilehash: 8358bf9a8eb0dab38b8f5847521e069f21fe4a2c
ms.sourcegitcommit: 09da3f26b4235368297b8b9b604d4282228a443c
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 07/28/2020
ms.locfileid: "87346700"
---
# <a name="mv-expand-operator"></a>Оператор mv-expand

Развертывает массив с несколькими значениями или контейнер свойств.

`mv-expand`применяется к столбцу в [динамическом](./scalar-data-types/dynamic.md)типизированном массиве или контейнере свойств, поэтому каждое значение в коллекции получает отдельную строку. Все остальные столбцы в развернутой строке дублируются. 

## <a name="syntax"></a>Синтаксис

*T* `| mv-expand ` [ `bagexpansion=` ( `bag`  |  `array` )] [ `with_itemindex=` *индексколумннаме*] *ColumnName* [ `,` *ColumnName* ...] [ `limit` *ровлимит*]

*T* `| mv-expand ` [ `bagexpansion=` ( `bag`  |  `array` )] [*имя* `=` ] *аррайекспрессион* [ `to typeof(` *TypeName* `)` ] [, [*имя* `=` ] *аррайекспрессион* [ `to typeof(` *TypeName* `)` ]...] [ `limit` *ровлимит*]

## <a name="arguments"></a>Аргументы

* *ColumnName* — в выходных данных массивы из этого столбца будут развернуты в несколько строк. 
* *ArrayExpression* — выражение, которое возвращает массив. Если используется этот аргумент, добавляется новый столбец, а существующий сохраняется.
* *Name* — имя нового столбца.
* *Имя типа:* Указывает базовый тип элементов массива, который станет типом столбца, созданного оператором. Несоответствующие значения в массиве не будут преобразованы. Вместо этого эти значения будут принимать `null` значение.
* *RowLimit* — максимальное количество строк, созданных из каждой исходной строки. Значение по умолчанию — 2147483647. 

  > [!Note]
  > Устаревшие и устаревшие формы оператора `mvexpand` имеют ограничение на число строк по умолчанию 128.

* *Индексколумннаме:* Если `with_itemindex` указан параметр, выходные данные будут включать в себя дополнительный столбец (с именем *индексколумннаме*), который содержит индекс (начиная с 0) элемента в исходной расширенной коллекции. 

## <a name="returns"></a>Результаты

Несколько строк для каждого значения в любом массиве, который находится в именованном столбце или в выражении массива.
Если указано несколько столбцов или выражений, они развертываются параллельно. Для каждой входной строки будет указано столько выходных строк, сколько есть элементов в максимальном развернутом выражении (более короткие списки дополнены значениями NULL). Если значение в строке является пустым массивом, строка расширяется до Nothing (не отображается в результирующем наборе). Однако если значение в строке не является массивом, строка сохраняется в результирующем наборе как есть. 

Тип развернутого столбца всегда динамический. Чтобы выполнить обычное или статистическое вычисление значений, используйте приведение, например `todatetime()` или `tolong()`.

Поддерживаются два режима развертывания контейнера свойств.
* `bagexpansion=bag`— контейнеры свойств развертываются в контейнеры свойств с одной записью. Этот режим является расширением по умолчанию.
* `bagexpansion=array`: Контейнеры свойств разворачиваются по `[` *key* `,` структурам массива*значений* ключей с двумя элементами `]` , что обеспечивает универсальный доступ к ключам и значениям (например, выполнение агрегата Distinct-Count для имен свойств). 

## <a name="examples"></a>Примеры

### <a name="single-column"></a>Один столбец

Простое расширение одного столбца:

<!-- csl: https://help.kusto.windows.net:443/Samples -->
 ```kusto
datatable (a:int, b:dynamic)[1,dynamic({"prop1":"a", "prop2":"b"})]
| mv-expand b 
```

|a|b|
|---|---|
|1|{"Prop1": "a"}|
|1|{"Prop2": "b"}|

### <a name="zipped-two-columns"></a>ZIP-архив двух столбцов

При раскрытии двух столбцов сначала будут почтовыми подходящими столбцами, а затем раскройте их:

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
datatable (a:int, b:dynamic, c:dynamic)[1,dynamic({"prop1":"a", "prop2":"b"}), dynamic([5, 4, 3])]
| mv-expand b, c
```

|a|b|с|
|---|---|---|
|1|{"Prop1": "a"}|5|
|1|{"Prop2": "b"}|4|
|1||3|

### <a name="cartesian-product-of-two-columns"></a>Декартово произведение двух столбцов

Если вы хотите получить декартово произведение двух столбцов, разверните один за другим:

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
datatable (a:int, b:dynamic, c:dynamic)[1,dynamic({"prop1":"a", "prop2":"b"}), dynamic([5])]
| mv-expand b 
| mv-expand c
```

|a|b|с|
|---|---|---|
|1|{"Prop1": "a"}|5|
|1|{"Prop2": "b"}|5|

### <a name="convert-output"></a>Преобразование вывода

Если требуется принудительно развернуть выходные данные MV до определенного типа (значение по умолчанию — Dynamic), используйте `to typeof` :

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
datatable (a:string, b:dynamic, c:dynamic)["Constant", dynamic([1,2,3,4]), dynamic([6,7,8,9])]
| mv-expand b, c to typeof(int)
| getschema 
```

ColumnName|ColumnOrdinal|DateType|ColumnType
-|-|-|-
a|0|System.String|строка
b|1|System.Object|Динамический
с|2|System.Int32|INT

Столбец "Обратите внимание" `b` будет выступающим, пока `dynamic` `c` не истекает `int` .

### <a name="using-with_itemindex"></a>Использование with_itemindex

Расширение массива с помощью `with_itemindex` :

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
range x from 1 to 4 step 1
| summarize x = make_list(x)
| mv-expand with_itemindex=Index x
```

|x|Индекс|
|---|---|
|1|0|
|2|1|
|3|2|
|4|3|
 
## <a name="see-also"></a>См. также

* Дополнительные примеры см. в разделе [Диаграмма число активных действий с](./samples.md#chart-concurrent-sessions-over-time) течением времени.
* Оператор « [MV-Apply»](./mv-applyoperator.md) .
* [обобщить make_list ()](makelist-aggfunction.md), которая является противоположной функцией MV-Expand.
* подключаемый модуль [bag_unpack ()](bag-unpackplugin.md) для расширения динамических объектов JSON в столбцы с помощью ключей контейнера свойств.
