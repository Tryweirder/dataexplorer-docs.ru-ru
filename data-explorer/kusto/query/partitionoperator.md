---
title: Оператор перегородки - Azure Data Explorer Документы Майкрософт
description: В этой статье описывается оператор разделов в Azure Data Explorer.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 02/13/2020
ms.openlocfilehash: 0a9ef31f68a989fffe42d9b54800e9305e51f4ed
ms.sourcegitcommit: 47a002b7032a05ef67c4e5e12de7720062645e9e
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 04/15/2020
ms.locfileid: "81511379"
---
# <a name="partition-operator"></a>Оператор partition

Оператор раздела делит свою таблицу ввода на несколько подтаблиц в соответствии с значениями указанного столбца, выполняет подзапрос над каждой подтаблицей и создает единую таблицу вывода, которая представляет собой объединение результатов всех подзапросов. 

```kusto
T | partition by Col1 ( top 10 by MaxValue )

T | partition by Col1 { U | where Col2=toscalar(Col1) }
```

**Синтаксис**

*T* `|` T `partition` -*Параметры раздела*- `by` *Колонка* `(` *КонтекстуальныеПодки*`)`

*T* `|` T `partition` -*Параметры раздела*- `by` *Подкачная* *колонка* `{``}`

**Аргументы**

* *T*: Табулярный источник, данные которого должны быть обработаны оператором.

* *Колонка*: Имя столбца в *T,* значения которого определяют, как должна быть разделена таблица ввода. Смотрите **Примечания** ниже.

* *ContextualSubquery*: Таблольное выражение, источник которого является источником `partition` оператора, прицелом для одного *ключевого* значения.

* *Подкачка*: Табулярное выражение без источника. *Ключевую* стоимость можно получить `toscalar()` по вызову.

* *Параметры раздела*: Ноль или более (пространственно-разделенные) параметры в виде: *Значение* *имени,* `=` контролирующее поведение оператора. Поддерживаются следующие параметры:

  |Имя               |Значения         |Описание|
  |-------------------|---------------|-----------|
  |`hint.materialized`|`true`,`false` |Если установлен `true` оматериализовать источник `partition` оператора (по умолчанию: `false`)|
  |`hint.concurrency`|*Номер*|Подсказки системе, сколько параллельных подзапросов `partition` оператора должно быть выполнено параллельно. *По умолчанию*: Количество ядер процессора на одном узло кластера (от 2 до 16).|
  |`hint.spread`|*Номер*|Подсказки системе, сколько узлов должно использоваться`partition` одновременное выполнение подзапросов. *По умолчанию*: 1.|

**Возвращает**

Оператор возвращает объединение результатов применения подзапроса к каждому разделу входных данных.

**Примечания**

* Оператор раздела в настоящее время ограничен количеством перегородок.
  Может быть создано до 64 различных разделов.
  Оператор даст ошибку, если столбец раздела *(колонка)* имеет более 64 различных значений.

* Подкачное отсылки на раздел ввода косвенно (в подвохе нет "имя" раздела). Чтобы соссылкой на раздел ввода несколько раз в подкачном, используйте [в качестве оператора,](asoperator.md)как в **примере: перегородка-ссылка** ниже.

**Пример: верхний корпус**

В некоторых случаях - это более performant `partition` и легче писать запрос с помощью оператора, `summarize` `top` а с помощью [ `top-nested` ](topnestedoperator.md) `W`оператора Следующий пример работает подзапрос расчета и для каждого из государств, начиная с : (ВИОМИНГ, ВАШИНГТОН, WEST VIRGINIA, WISCONSIN)

```kusto
StormEvents
| where State startswith 'W'
| partition by State 
(
    summarize Events=count(), Injuries=sum(InjuriesDirect) by EventType, State
    | top 3 by Events 
) 

```
|EventType|Состояние|События|Травм|
|---|---|---|---|
|Град|Вайоминг|108|0|
|Высокий ветер|Вайоминг|81|5|
|Зимний шторм|Вайоминг|72|0|
|Тяжелый снег|Вашингтон|82|0|
|Высокий ветер|Вашингтон|58|13|
|Лесной пожар|Вашингтон|29|0|
|Шквалистый ветер|ЗАПАДНАЯ ВИРДЖИНИЯ|180|1|
|Град|ЗАПАДНАЯ ВИРДЖИНИЯ|103|0|
|Зимняя погода|ЗАПАДНАЯ ВИРДЖИНИЯ|88|0|
|Шквалистый ветер|Висконсин|416|1|
|Зимний шторм|Висконсин|310|0|
|Град|Висконсин|303|1|

**Пример: неперекрывающиеся разделы данных**

Иногда полезно (perf-wise) запускать сложную подзаготовительную систему над неперекрывающимися разделами данных в стиле карты/уменьшения. Приведенный ниже пример показывает, как создать ручное распределение агрегатов на 10 разделов.

```kusto
StormEvents
| extend p = hash(EventId, 10)
| partition by p
(
    summarize Count=count() by Source 
)
| summarize Count=sum(Count) by Source
| top 5 by Count
```

|Источник|Count|
|---|---|
|Подготовленный корректировщик|12770|
|Правоприменение|8570|
|Public|6157|
|Специалист по управлению в чрезвычайных ситуациях|4900|
|КООПЕРАТИВ наблюдателЯ|3039|

**Пример: раздел времени запроса**

В следующем примере показано, как запрос может быть разделен на разделы N-10, где каждый раздел вычисляет свой собственный Count, а затем суммируется в TotalCount.

```kusto
let N = 10;                 // Number of query-partitions
range p from 0 to N-1 step 1  // 
| partition by p            // Run the sub-query partitioned 
{
    StormEvents 
    | where hash(EventId, N) == toscalar(p) // Use toscalar() to fetch partition key value
    | summarize Count = count()
}
| summarize TotalCount=sum(Count) 
```

|TotalCount|
|---|
|59066|


**Пример: перегородка-ссылка**

В следующем примере показано, как можно использовать [в качестве оператора,](asoperator.md) чтобы дать "имя" для каждого раздела данных, а затем повторно использовать это имя в подзапросе:

```kusto
T
| partition by Dim
(
    as Partition
    | extend MetricPct = Metric * 100.0 / toscalar(Partition | summarize sum(Metric))
)
```

**Пример: сложный подкатый, скрытый вызовом функции**

Тот же метод может быть применен с гораздо более сложными подзапросами. Чтобы упростить синтаксис, можно обернуть subquery в вызов функции:

```kusto
let partition_function = (T:(Source:string)) 
{
    T
    | summarize Count=count() by Source
};
StormEvents
| extend p = hash(EventId, 10)
| partition by p
(
    invoke partition_function()
)
| summarize Count=sum(Count) by Source
| top 5 by Count
```

|Источник|Count|
|---|---|
|Подготовленный корректировщик|12770|
|Правоприменение|8570|
|Public|6157|
|Специалист по управлению в чрезвычайных ситуациях|4900|
|КООПЕРАТИВ наблюдателЯ|3039|