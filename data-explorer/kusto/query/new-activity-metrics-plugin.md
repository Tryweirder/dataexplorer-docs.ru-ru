---
title: new_activity_metrics плагин - Azure Data Explorer (ru) Документы Майкрософт
description: В этой статье описывается new_activity_metrics плагин в Azure Data Explorer.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 03/30/2020
ms.openlocfilehash: 0aad1c91fec6855030544596a08818b80bbf3d18
ms.sourcegitcommit: 47a002b7032a05ef67c4e5e12de7720062645e9e
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 04/15/2020
ms.locfileid: "81512212"
---
# <a name="new_activity_metrics-plugin"></a>Подключаемый модуль new_activity_metrics

Вычисляет полезные метрики активности (различные значения подсчета, четкое количество новых `New Users`значений, скорость удержания и скорость оттока) для когорты . Каждая когорта `New Users` (все пользователи, которые были 1-м видели во времени окно) по сравнению со всеми предыдущими когортами. Сравнение учитывает *все* предыдущие временные окна. Например, в записи для от Т2 и до Т3, отчетливое количество пользователей будет все пользователи в T3, которые не были замечены в обоих T1 и T2. 
```kusto
T | evaluate new_activity_metrics(id, datetime_column, startofday(ago(30d)), startofday(now()), 1d, dim1, dim2, dim3)
```

**Синтаксис**

*T* `| evaluate` *End* `,` *Window* `,` `,` `,` `,` *IdColumn* `,` `,` `,` *Start* *dim2* *Cohort* *dim1* *TimelineColumn* IdColumn TimelineКолонка Стартовый конец окна » Когорта »Dim1 dim2 ...» `new_activity_metrics(` `,` *Оглядывай назад*`)`

**Аргументы**

* *T*: Входное табеляцивское выражение.
* *IdColumn*: Название столбца с значениями идентификатора, которые представляют активность пользователя. 
* *TimelineColumn*: Название столбца, представляющего временную шкалу.
* *Начало*: Scalar со значением периода начала анализа.
* *Конец*: Scalar со значением конечного периода анализа.
* *Окно*: Scalar со значением периода окна анализа. Может быть либо числовая / дата / timestamp значение, `week` / `month` / `year`или строка, которая является одним из , и в этом случае все периоды будут [startofweek](startofweekfunction.md)/[startofmonth](startofmonthfunction.md)/[startofyear](startofyearfunction.md) соответственно. 
* *Когорта*: (по желанию) скаларная константа с указанием конкретной когорты. Если не предусмотрено, все когорты, соответствующие окну времени анализа, рассчитываются и возвращаются.
* *dim1*, *dim2*, ...: (необязательный) список столбцов измерений, которые нарезает расчет метрик активности.
* *Lookback*: (необязательно) табулярное выражение с набором идонужнок, которые относятся к периоду "оглянуться назад"

**Возвращает**

Возвращает таблицу с различными значениями подсчета, определенным подсчетом новых значений, коэффициентом удержания и коэффициентом оттока для каждой комбинации периодов "от" и "к" временной шкалы и для каждой существующей комбинации измерений.

Схема таблицы вывода:

|from_TimelineColumn|to_TimelineColumn|dcount_new_values|dcount_retained_values|dcount_churn_values|retention_rate|churn_rate|dim1|..|dim_n|
|---|---|---|---|---|---|---|---|---|---|
|тип: по состоянию на *ХроникаColumn*|—||—|long|long|double|double|double|..|..|..|

* `from_TimelineColumn`- когорта новых пользователей. Метрика в этой записи относится ко всем пользователям, которые были впервые замечены в этот период. Решение о *первом просмотре* принимается во внимание все предыдущие периоды в период анализа. 
* `to_TimelineColumn`- период, сравниваемый с. 
* `dcount_new_values`- количество различных `to_TimelineColumn` пользователей, в которых не были `from_TimelineColumn`замечены во *все* периоды до и в том числе . 
* `dcount_retained_values`- из всех новых пользователей, впервые видели в `from_TimelineColumn`, `to_TimelineCoumn`количество различных пользователей, которые были замечены в .
* `dcount_churn_values`- из всех новых пользователей, впервые замеченных в `from_TimelineColumn`, количество различных пользователей, которые *не* были замечены в `to_TimelineCoumn`.
* `retention_rate`- процент `dcount_retained_values` из когорты (пользователи впервые видели в `from_TimelineColumn`).
* `churn_rate`- процент `dcount_churn_values` из когорты (пользователи впервые видели в `from_TimelineColumn`).

**Примечания**

Для определения `Retention Rate` и `Churn Rate` - ссылайтесь на раздел **Заметки** в [activity_metrics плагиндокументации.](./activity-metrics-plugin.md)


**Примеры**

Следующий набор данных образца показывает, какие пользователи видели, в какие дни. Таблица была создана `Users` на основе исходной таблицы следующим образом: 

```kusto
Users | summarize tostring(make_set(user)) by bin(Timestamp, 1d) | order by Timestamp asc;
```

|Отметка времени|set_user|
|---|---|
|2019-11-01 00:00:00.0000000|[0,2,3,4]|
|2019-11-02 00:00:00.0000000|[0,1,3,4,5]|
|2019-11-03 00:00:00.0000000|[0,2,4,5]|
|2019-11-04 00:00:00.0000000|[0,1,2,3]|
|2019-11-05 00:00:00.0000000|[0,1,2,3,4]|

Выход плагина для исходной таблицы следующий: 

```kusto
let StartDate = datetime(2019-11-01 00:00:00);
let EndDate = datetime(2019-11-07 00:00:00);
Users 
| evaluate new_activity_metrics(user, Timestamp, StartDate, EndDate-1tick, 1d) 
| where from_Timestamp < datetime(2019-11-03 00:00:00.0000000)
```

|R|from_Timestamp|to_Timestamp|dcount_new_values|dcount_retained_values|dcount_churn_values|retention_rate|churn_rate|
|---|---|---|---|---|---|---|---|
|1|2019-11-01 00:00:00.0000000|2019-11-01 00:00:00.0000000|4|4|0|1|0|
|2|2019-11-01 00:00:00.0000000|2019-11-02 00:00:00.0000000|2|3|1|0.75|0,25|
|3|2019-11-01 00:00:00.0000000|2019-11-03 00:00:00.0000000|1|3|1|0.75|0,25|
|4|2019-11-01 00:00:00.0000000|2019-11-04 00:00:00.0000000|1|3|1|0.75|0,25|
|5|2019-11-01 00:00:00.0000000|2019-11-05 00:00:00.0000000|1|4|0|1|0|
|6|2019-11-01 00:00:00.0000000|2019-11-06 00:00:00.0000000|0|0|4|0|1|
|7|2019-11-02 00:00:00.0000000|2019-11-02 00:00:00.0000000|2|2|0|1|0|
|8|2019-11-02 00:00:00.0000000|2019-11-03 00:00:00.0000000|0|1|1|0,5|0,5|
|9|2019-11-02 00:00:00.0000000|2019-11-04 00:00:00.0000000|0|1|1|0,5|0,5|
|10|2019-11-02 00:00:00.0000000|2019-11-05 00:00:00.0000000|0|1|1|0,5|0,5|
|11|2019-11-02 00:00:00.0000000|2019-11-06 00:00:00.0000000|0|0|2|0|1|

Ниже приводится анализ нескольких записей из вывода: 
* Запись `R=3` `from_TimelineColumn`  =  `2019-11-01`, `to_TimelineColumn`  = , `2019-11-03`:
    * Все новые пользователи, замеченные на 11/1, являются пользователями, рассматриваемыми для этой записи. Так как это первый период, это все пользователи в этой корзине - 0,2,3,4
    * `dcount_new_values`- количество пользователей на 11/3, которые не были замечены на 11/1. Это включает в `5`себя одного пользователя - . 
    * `dcount_retained_values`- из всех новых пользователей на 11/1, сколько были сохранены до 11/3? Есть три`[0,2,4]`( `count_churn_values` ), в то`3`время как один (пользователь ) ). 
    * `retention_rate`0,75 - три сохраненных пользователей из четырех новых пользователей, которые были впервые замечены в 11/1. 

* Запись `R=9` `from_TimelineColumn`  =  `2019-11-02`, `to_TimelineColumn`  = , `2019-11-04`:
    * Эта запись фокусируется на новых пользователей, которые были `1` впервые замечены на 11'2 - пользователи и `5`. 
    * `dcount_new_values`- количество пользователей на 11/4, которые не `T0 .. from_Timestamp`были замечены на протяжении всех периодов . Это означает, что пользователи, которые видели на 11'4, но которые не были замечены ни на 11/1 или 11'2 - Есть нет таких пользователей. 
    * `dcount_retained_values`- из всех новых пользователей на`[1,5]`11'2 ( ), сколько были сохранены до 11/4? Там один такой пользователь`[1]`(), в то `5`время как count_churn_values является одним (пользователь ). 
    * `retention_rate`0,5 - единственный пользователь, который был сохранен на 11/4 из двух новых на 11'2. 


### <a name="weekly-retention-rate-and-churn-rate-single-week"></a>Еженедельный коэффициент удержания и коэффициент оттока (одна неделя)

Следующий запрос вычисляет скорость удержания и оттока `New Users` для недельного окна для когорты (пользователей, прибывших в первую неделю).

```kusto
// Generate random data of user activities
let _start = datetime(2017-05-01);
let _end = datetime(2017-05-31);
range Day from _start to _end  step 1d
| extend d = tolong((Day - _start)/1d)
| extend r = rand()+1
| extend _users=range(tolong(d*50*r), tolong(d*50*r+200*r-1), 1) 
| mv-expand id=_users to typeof(long) limit 1000000
// Take only the first week cohort (last parameter)
| evaluate new_activity_metrics(['id'], Day, _start, _end, 7d, _start)
| project from_Day, to_Day, retention_rate, churn_rate
```

|from_Day|to_Day|retention_rate|churn_rate|
|---|---|---|---|
|2017-05-01 00:00:00.0000000|2017-05-01 00:00:00.0000000|1|0|
|2017-05-01 00:00:00.0000000|2017-05-08 00:00:00.0000000|0.544632768361582|0.455367231638418|
|2017-05-01 00:00:00.0000000|2017-05-15 00:00:00.0000000|0.031638418079096|0.968361581920904|
|2017-05-01 00:00:00.0000000|2017-05-22 00:00:00.0000000|0|1|
|2017-05-01 00:00:00.0000000|2017-05-29 00:00:00.0000000|0|1|


### <a name="weekly-retention-rate-and-churn-rate-complete-matrix"></a>Еженедельный коэффициент удержания и коэффициент оттока (полная матрица)

Следующий запрос вычисляет скорость удержания и оттока для окна за неделю для `New Users` когорты. Если предыдущий пример вычислил статистику за одну неделю - ниже приводится таблица NxN для каждой из/к комбинации.

```kusto
// Generate random data of user activities
let _start = datetime(2017-05-01);
let _end = datetime(2017-05-31);
range Day from _start to _end  step 1d
| extend d = tolong((Day - _start)/1d)
| extend r = rand()+1
| extend _users=range(tolong(d*50*r), tolong(d*50*r+200*r-1), 1) 
| mv-expand id=_users to typeof(long) limit 1000000
// Last parameter is omitted - 
| evaluate new_activity_metrics(['id'], Day, _start, _end, 7d)
| project from_Day, to_Day, retention_rate, churn_rate
```

|from_Day|to_Day|retention_rate|churn_rate|
|---|---|---|---|
|2017-05-01 00:00:00.0000000|2017-05-01 00:00:00.0000000|1|0|
|2017-05-01 00:00:00.0000000|2017-05-08 00:00:00.0000000|0.190397350993377|0.809602649006622|
|2017-05-01 00:00:00.0000000|2017-05-15 00:00:00.0000000|0|1|
|2017-05-01 00:00:00.0000000|2017-05-22 00:00:00.0000000|0|1|
|2017-05-01 00:00:00.0000000|2017-05-29 00:00:00.0000000|0|1|
|2017-05-08 00:00:00.0000000|2017-05-08 00:00:00.0000000|1|0|
|2017-05-08 00:00:00.0000000|2017-05-15 00:00:00.0000000|0.405263157894737|0.594736842105263|
|2017-05-08 00:00:00.0000000|2017-05-22 00:00:00.0000000|0.227631578947368|0.772368421052632|
|2017-05-08 00:00:00.0000000|2017-05-29 00:00:00.0000000|0|1|
|2017-05-15 00:00:00.0000000|2017-05-15 00:00:00.0000000|1|0|
|2017-05-15 00:00:00.0000000|2017-05-22 00:00:00.0000000|0.785488958990536|0.214511041009464|
|2017-05-15 00:00:00.0000000|2017-05-29 00:00:00.0000000|0.237644584647739|0.762355415352261|
|2017-05-22 00:00:00.0000000|2017-05-22 00:00:00.0000000|1|0|
|2017-05-22 00:00:00.0000000|2017-05-29 00:00:00.0000000|0.621835443037975|0.378164556962025|
|2017-05-29 00:00:00.0000000|2017-05-29 00:00:00.0000000|1|0|


### <a name="weekly-retention-rate-with-lookback-period"></a>Недельный коэффициент удержания с периодом возврата

Следующий запрос вычисляет `New Users` скорость удержания когорты при учете `lookback` периода: табелистский запрос с `New Users` набором идентификаторов, которые используются `New Users`для определения когорты (все идентификаторы, которые не отображаются в этом наборе, являются). Запрос исследует поведение удержания `New Users` в период анализа.

```kusto
// Generate random data of user activities
let _lookback = datetime(2017-02-01);
let _start = datetime(2017-05-01);
let _end = datetime(2017-05-31);
let _data = range Day from _lookback to _end  step 1d
| extend d = tolong((Day - _lookback)/1d)
| extend r = rand()+1
| extend _users=range(tolong(d*50*r), tolong(d*50*r+200*r-1), 1) 
| mv-expand id=_users to typeof(long) limit 1000000;
//
let lookback_data = _data | where Day < _start | project Day, id;
_data
| evaluate new_activity_metrics(id, Day, _start, _end, 7d, _start, lookback_data)
| project from_Day, to_Day, retention_rate
```

|from_Day|to_Day|retention_rate|
|---|---|---|
|2017-05-01 00:00:00.0000000|2017-05-01 00:00:00.0000000|1|
|2017-05-01 00:00:00.0000000|2017-05-08 00:00:00.0000000|0.404081632653061|
|2017-05-01 00:00:00.0000000|2017-05-15 00:00:00.0000000|0.257142857142857|
|2017-05-01 00:00:00.0000000|2017-05-22 00:00:00.0000000|0.296326530612245|
|2017-05-01 00:00:00.0000000|2017-05-29 00:00:00.0000000|0.0587755102040816|
