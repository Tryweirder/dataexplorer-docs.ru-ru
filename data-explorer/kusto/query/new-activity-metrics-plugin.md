---
title: подключаемый модуль new_activity_metrics — Azure обозреватель данных
description: В этой статье описывается подключаемый модуль new_activity_metrics в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: alexans
ms.service: data-explorer
ms.topic: reference
ms.date: 03/30/2020
ms.openlocfilehash: 447191158ce3de69c4429b5af4a33d24150af77c
ms.sourcegitcommit: 608539af6ab511aa11d82c17b782641340fc8974
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 10/20/2020
ms.locfileid: "92248854"
---
# <a name="new_activity_metrics-plugin"></a>Подключаемый модуль new_activity_metrics

Вычисляет полезные метрики действий (значения числа различных объектов, число различных значений, скорость хранения и частоту обновлений) для когорту `New Users` . Каждый когорту `New Users` (все пользователи, которые были видны в временном окне) сравниваются со всеми более ранними когорты. При сравнении учитываются *все* предыдущие окна времени. Например, в записи от = T2 и до = T3 число пользователей будет составлять все пользователи в T3, которые не были видны как в T1, так и в T2. 
```kusto
T | evaluate new_activity_metrics(id, datetime_column, startofday(ago(30d)), startofday(now()), 1d, dim1, dim2, dim3)
```

## <a name="syntax"></a>Синтаксис

*T* `| evaluate` `new_activity_metrics(` *идколумн* `,` *тимелинеколумн* `,` *Start* `,` *End* `,` *Window* [ `,` *когорту*] [ `,` *Dim1* `,` *dim2* `,` ...] [ `,` *лукбакк*]`)`

## <a name="arguments"></a>Аргументы

* *T*: Входное табличное выражение.
* *Идколумн*: имя столбца со значениями идентификаторов, представляющими действия пользователя. 
* *Тимелинеколумн*: имя столбца, представляющего временную шкалу.
* *Начало*: скалярное значение начального периода анализа.
* *End*: скаляр со значением периода окончания анализа.
* *Window*: скалярный со значением периода окна анализа. Может быть либо числовым, либо значением DateTime или timestamp, либо строкой, которая является одним из `week` / `month` / `year` , в этом случае все периоды будут [startofweek](startofweekfunction.md) / [StartOfMonth](startofmonthfunction.md) / [startofyear](startofyearfunction.md) соответствующим образом. 
* *Когорту*: (необязательно) скалярная константа, указывающая конкретный когорту. Если не указано, вычисляются и возвращаются все когорты, соответствующие окну времени анализа.
* *Dim1*, *dim2*,...: (необязательно) список столбцов измерений, которые срезируют вычисление метрик действия.
* *Лукбакк*(необязательно) табличное выражение с набором идентификаторов, принадлежащих периоду "Поиск".

## <a name="returns"></a>Результаты

Возвращает таблицу, имеющую значения числа различных объектов, общее число новых значений, частоту хранения и частоту обновления для каждого сочетания точек временной шкалы "от" и "до", а также для всех существующих комбинаций измерений.

Схема выходной таблицы:

|from_TimelineColumn|to_TimelineColumn|dcount_new_values|dcount_retained_values|dcount_churn_values|retention_rate|churn_rate|Dim1|..|dim_n|
|---|---|---|---|---|---|---|---|---|---|
|Тип: от *тимелинеколумн*|—||—|long|long|double|double|double|..|..|..|

* `from_TimelineColumn` — когорту новых пользователей. Метрики в этой записи относятся ко всем пользователям, которые впервые видели в этом периоде. При принятии решения о *первом рассмотрении* учитываются все предыдущие периоды в периоде анализа. 
* `to_TimelineColumn` — период, по которому сравнивается. 
* `dcount_new_values` — число уникальных пользователей `to_TimelineColumn` , в которых не были видны *все* периоды до и включительно `from_TimelineColumn` . 
* `dcount_retained_values` — от всех новых пользователей, впервые отображаемых в `from_TimelineColumn` , количество уникальных пользователей, которые были в `to_TimelineCoumn` .
* `dcount_churn_values` — от всех новых пользователей, впервые отображаемых в `from_TimelineColumn` , количество уникальных пользователей, которые *не* были обнаружены в `to_TimelineCoumn` .
* `retention_rate` — процент от `dcount_retained_values` когорту (пользователей, впервые просмотренных в `from_TimelineColumn` ).
* `churn_rate` — процент от `dcount_churn_values` когорту (пользователей, впервые просмотренных в `from_TimelineColumn` ).

**Примечания**

Для определения `Retention Rate` и `Churn Rate` — см. раздел " **Примечания** " в документации по [подключаемому модулю activity_metrics](./activity-metrics-plugin.md) .


## <a name="examples"></a>Примеры

В следующем образце набора данных показано, какие пользователи видят дни. Таблица была создана на основе исходной `Users` таблицы следующим образом: 

```kusto
Users | summarize tostring(make_set(user)) by bin(Timestamp, 1d) | order by Timestamp asc;
```

|Отметка времени|set_user|
|---|---|
|2019-11-01 00:00:00.0000000|[0, 2, 3, 4]|
|2019-11-02 00:00:00.0000000|[0, 1, 3, 4, 5]|
|2019-11-03 00:00:00.0000000|[0, 2, 4, 5]|
|2019-11-04 00:00:00.0000000|[0, 1, 2, 3]|
|2019-11-05 00:00:00.0000000|[0, 1, 2, 3, 4]|

Ниже приведены выходные данные подключаемого модуля для исходной таблицы. 

```kusto
let StartDate = datetime(2019-11-01 00:00:00);
let EndDate = datetime(2019-11-07 00:00:00);
Users 
| evaluate new_activity_metrics(user, Timestamp, StartDate, EndDate-1tick, 1d) 
| where from_Timestamp < datetime(2019-11-03 00:00:00.0000000)
```

|R|from_Timestamp|to_Timestamp|dcount_new_values|dcount_retained_values|dcount_churn_values|retention_rate|churn_rate|
|---|---|---|---|---|---|---|---|
|1|2019-11-01 00:00:00.0000000|2019-11-01 00:00:00.0000000|4|4|0|1|0|
|2|2019-11-01 00:00:00.0000000|2019-11-02 00:00:00.0000000|2|3|1|0,75|0,25|
|3|2019-11-01 00:00:00.0000000|2019-11-03 00:00:00.0000000|1|3|1|0,75|0,25|
|4|2019-11-01 00:00:00.0000000|2019-11-04 00:00:00.0000000|1|3|1|0,75|0,25|
|5|2019-11-01 00:00:00.0000000|2019-11-05 00:00:00.0000000|1|4|0|1|0|
|6|2019-11-01 00:00:00.0000000|2019-11-06 00:00:00.0000000|0|0|4|0|1|
|7|2019-11-02 00:00:00.0000000|2019-11-02 00:00:00.0000000|2|2|0|1|0|
|8|2019-11-02 00:00:00.0000000|2019-11-03 00:00:00.0000000|0|1|1|0,5|0,5|
|9|2019-11-02 00:00:00.0000000|2019-11-04 00:00:00.0000000|0|1|1|0,5|0,5|
|10|2019-11-02 00:00:00.0000000|2019-11-05 00:00:00.0000000|0|1|1|0,5|0,5|
|11|2019-11-02 00:00:00.0000000|2019-11-06 00:00:00.0000000|0|0|2|0|1|

Ниже приведен анализ нескольких записей из выходных данных. 
* Запись `R=3` , `from_TimelineColumn`  =  `2019-11-01` , `to_TimelineColumn`  =  `2019-11-03` :
    * Пользователи, рассматриваемые для этой записи, являются новыми пользователями, которые видят 11/1. Так как это первый период, все пользователи в этой ячейке — [0, 2, 3, 4]
    * `dcount_new_values` — число пользователей в 11/3, которые не видели 11/1. Сюда входит один пользователь — `5` . 
    * `dcount_retained_values` — от всех новых пользователей на 11/1, сколько было сохраниться до 11/3? Существует три ( `[0,2,4]` ), а `count_churn_values` — один (User = `3` ). 
    * `retention_rate` = 0,75 — три пользователя, не вошедшие в число новых пользователей, которые впервые видели в 11/1. 

* Запись `R=9` , `from_TimelineColumn`  =  `2019-11-02` , `to_TimelineColumn`  =  `2019-11-04` :
    * Эта запись посвящена новым пользователям, которые впервые видели в 11/2 – Users `1` and `5` . 
    * `dcount_new_values` — число пользователей в 11/4, которые не были видны за все периоды `T0 .. from_Timestamp` . Это означает, что пользователи, которые видят 11/4, но не видели ни 11/1, ни 11/2, таких пользователей нет. 
    * `dcount_retained_values` — от всех новых пользователей на 11/2 ( `[1,5]` ), сколько было сохраниться до 11/4? Существует один пользователь ( `[1]` ), а count_churn_values — один (пользователь `5` ). 
    * `retention_rate` — 0,5 — один пользователь, который был сохранен на 11/4 из двух новых в 11/2. 


### <a name="weekly-retention-rate-and-churn-rate-single-week"></a>Еженедельный темп хранения и частота обновлений (одна неделя)

Следующий запрос вычисляет частоту хранения и обновления для недельного окна для `New Users` когорту (пользователей, поступивших в первую неделю).

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
// Generate random data of user activities
let _start = datetime(2017-05-01);
let _end = datetime(2017-05-31);
range Day from _start to _end  step 1d
| extend d = tolong((Day - _start)/1d)
| extend r = rand()+1
| extend _users=range(tolong(d*50*r), tolong(d*50*r+200*r-1), 1) 
| mv-expand id=_users to typeof(long) limit 1000000
// Take only the first week cohort (last parameter)
| evaluate new_activity_metrics(['id'], Day, _start, _end, 7d, _start)
| project from_Day, to_Day, retention_rate, churn_rate
```

|from_Day|to_Day|retention_rate|churn_rate|
|---|---|---|---|
|2017-05-01 00:00:00.0000000|2017-05-01 00:00:00.0000000|1|0|
|2017-05-01 00:00:00.0000000|2017-05-08 00:00:00.0000000|0.544632768361582|0.455367231638418|
|2017-05-01 00:00:00.0000000|2017-05-15 00:00:00.0000000|0.031638418079096|0.968361581920904|
|2017-05-01 00:00:00.0000000|2017-05-22 00:00:00.0000000|0|1|
|2017-05-01 00:00:00.0000000|2017-05-29 00:00:00.0000000|0|1|


### <a name="weekly-retention-rate-and-churn-rate-complete-matrix"></a>Еженедельный темп хранения и частота обновлений (Полная матрица)

Следующий запрос вычисляет скорость хранения и обновления для недельного окна в течение недели для `New Users` когорту. Если в предыдущем примере статистика была рассчитана за одну неделю, то ниже создается таблица Нксн для каждого сочетания from/to.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
// Generate random data of user activities
let _start = datetime(2017-05-01);
let _end = datetime(2017-05-31);
range Day from _start to _end  step 1d
| extend d = tolong((Day - _start)/1d)
| extend r = rand()+1
| extend _users=range(tolong(d*50*r), tolong(d*50*r+200*r-1), 1) 
| mv-expand id=_users to typeof(long) limit 1000000
// Last parameter is omitted - 
| evaluate new_activity_metrics(['id'], Day, _start, _end, 7d)
| project from_Day, to_Day, retention_rate, churn_rate
```

|from_Day|to_Day|retention_rate|churn_rate|
|---|---|---|---|
|2017-05-01 00:00:00.0000000|2017-05-01 00:00:00.0000000|1|0|
|2017-05-01 00:00:00.0000000|2017-05-08 00:00:00.0000000|0.190397350993377|0.809602649006622|
|2017-05-01 00:00:00.0000000|2017-05-15 00:00:00.0000000|0|1|
|2017-05-01 00:00:00.0000000|2017-05-22 00:00:00.0000000|0|1|
|2017-05-01 00:00:00.0000000|2017-05-29 00:00:00.0000000|0|1|
|2017-05-08 00:00:00.0000000|2017-05-08 00:00:00.0000000|1|0|
|2017-05-08 00:00:00.0000000|2017-05-15 00:00:00.0000000|0.405263157894737|0.594736842105263|
|2017-05-08 00:00:00.0000000|2017-05-22 00:00:00.0000000|0.227631578947368|0.772368421052632|
|2017-05-08 00:00:00.0000000|2017-05-29 00:00:00.0000000|0|1|
|2017-05-15 00:00:00.0000000|2017-05-15 00:00:00.0000000|1|0|
|2017-05-15 00:00:00.0000000|2017-05-22 00:00:00.0000000|0.785488958990536|0.214511041009464|
|2017-05-15 00:00:00.0000000|2017-05-29 00:00:00.0000000|0.237644584647739|0.762355415352261|
|2017-05-22 00:00:00.0000000|2017-05-22 00:00:00.0000000|1|0|
|2017-05-22 00:00:00.0000000|2017-05-29 00:00:00.0000000|0.621835443037975|0.378164556962025|
|2017-05-29 00:00:00.0000000|2017-05-29 00:00:00.0000000|1|0|


### <a name="weekly-retention-rate-with-lookback-period"></a>Еженедельный темп хранения с лукбакк периодом

Следующий запрос вычисляет частоту хранения когорту при отсчете `New Users` периода в расчете `lookback` : табличный запрос с набором идентификаторов, который используется для определения `New Users` когорту (все идентификаторы, которые не отображаются в этом наборе `New Users` ). Запрос проверяет поведение при хранении в `New Users` течение периода анализа.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
// Generate random data of user activities
let _lookback = datetime(2017-02-01);
let _start = datetime(2017-05-01);
let _end = datetime(2017-05-31);
let _data = range Day from _lookback to _end  step 1d
| extend d = tolong((Day - _lookback)/1d)
| extend r = rand()+1
| extend _users=range(tolong(d*50*r), tolong(d*50*r+200*r-1), 1) 
| mv-expand id=_users to typeof(long) limit 1000000;
//
let lookback_data = _data | where Day < _start | project Day, id;
_data
| evaluate new_activity_metrics(id, Day, _start, _end, 7d, _start, lookback_data)
| project from_Day, to_Day, retention_rate
```

|from_Day|to_Day|retention_rate|
|---|---|---|
|2017-05-01 00:00:00.0000000|2017-05-01 00:00:00.0000000|1|
|2017-05-01 00:00:00.0000000|2017-05-08 00:00:00.0000000|0.404081632653061|
|2017-05-01 00:00:00.0000000|2017-05-15 00:00:00.0000000|0.257142857142857|
|2017-05-01 00:00:00.0000000|2017-05-22 00:00:00.0000000|0.296326530612245|
|2017-05-01 00:00:00.0000000|2017-05-29 00:00:00.0000000|0.0587755102040816|
