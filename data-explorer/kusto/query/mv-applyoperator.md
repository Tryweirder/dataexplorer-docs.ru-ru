---
title: Оператор MV-Apply — Azure обозреватель данных
description: В этой статье описывается оператор MV-Apply в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 02/13/2020
ms.openlocfilehash: 80e38c1782a4476181fe73c5f77d6460f2ef539f
ms.sourcegitcommit: 733bde4c6bc422c64752af338b29cd55a5af1f88
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 05/13/2020
ms.locfileid: "83271236"
---
# <a name="mv-apply-operator"></a>Оператор mv-apply

`mv-apply`Оператор разворачивает каждую запись во входной таблице в подтаблице, применяет вложенный запрос к каждой подтаблице и возвращает объединение результатов всех вложенных запросов.

Например, предположим, что `T` у таблицы есть столбец `Metric` типа `dynamic` , значения которого являются массивами `real` чисел. Следующий запрос найдет два крупнейших значения в каждом `Metric` значении и возвратит записи, соответствующие этим значениям.

```kusto
T | mv-apply Metric to typeof(real) on (top 2 by Metric desc)
```

`mv-apply`Оператор имеет следующие этапы обработки:

1. Использует [`mv-expand`](./mvexpandoperator.md) оператор, чтобы развернуть каждую запись во входных данных в подтаблицах.
1. Применяет вложенный запрос для каждой подтаблицы.
1. Добавляет ноль или более столбцов в результирующую подтаблицу. Эти столбцы содержат значения исходных столбцов, которые не развернуты, и при необходимости повторяются.
1. Возвращает объединение результатов.

`mv-expand`Оператор получает следующие входные данные:

1. Одно или несколько выражений, которые вычисляют динамические массивы для расширения.
   Количество записей в каждой развернутой подтаблице равно максимальной длине каждого из этих динамических массивов. Значения NULL добавляются, если указано несколько выражений и соответствующие массивы имеют разную длину.

1. При необходимости имена присваиваются значениям выражений после расширения.
   Эти имена становятся именами столбцов в подтаблицах.
   Если это значение не указано, используется исходное имя столбца, если выражение является ссылкой на столбец. В противном случае используется случайное имя. 

   > [!NOTE]
   > Рекомендуется использовать имена столбцов по умолчанию.

1. Типы данных элементов этих динамических массивов после расширения.
   Они становятся типами столбцов в подтаблицах.
   Если этот параметр не задан, используется значение `dynamic`.

1. (Необязательно) имя столбца, добавляемого в подтаблицы, указывающего Отсчитываемый от нуля индекс элемента в массиве, который привел к записи подтаблицы.

1. При необходимости максимальное число элементов массива, которое нужно развернуть.

`mv-apply`Оператор может рассматриваться как обобщение [`mv-expand`](./mvexpandoperator.md) оператора (на самом деле, второй может быть реализован первым, если вложенный запрос включает только проекции).

**Синтаксис**

*T* `|` `mv-apply` [*итеминдекс*] *колумнстоекспанд* [*ровлимит*] `on` `(` *вложенный запрос*`)`

Где *итеминдекс* имеет синтаксис:

`with_itemindex``=` *Индексколумннаме*

*Колумнстоекспанд* — это список из одного или нескольких элементов в форме с разделителями-запятыми:

[*Имя* `=` ] *Аррайекспрессион* [ `to` `typeof` `(` *TypeName* `)` ]

*Ровлимит* — это просто:

`limit`*Ровлимит*

и *вложенный запрос* имеют тот же синтаксис, что и любая инструкция запроса.

**Аргументы**

* *Итеминдекс*: Если используется, указывает имя столбца типа `long` , который добавляется к входным данным как часть фазы расширения массива и указывает индекс массива, основанного на 0, для развернутого значения.

* *Имя*: Если используется, имя назначает развернутые массивы значений каждого выражения, развернутого в массиве.
  Если значение не указано, будет использоваться имя столбца, если оно доступно.
  Случайное имя создается, если *аррайекспрессион* не является простым именем столбца.

* *Аррайекспрессион*: выражение типа `dynamic` , значения которого будут развернуты в массиве.
  Если выражение является именем столбца во входных данных, то входной столбец удаляется из входных данных, а в выходных данных появляется новый столбец с тем же именем (или *ColumnName* , если он указан).

* *TypeName*: Если используется, то имя типа, который принимают отдельные элементы `dynamic` массива *аррайекспрессион* . Элементы, которые не соответствуют этому типу, будут заменены значением NULL.
  (Если не указано, `dynamic` используется по умолчанию.)

* *Ровлимит*: при использовании ограничивает количество записей, создаваемых на основе каждой записи входных данных.
  (Если не указано, используется 2147483647.)

* *Вложенный запрос*: табличное выражение запроса с неявным табличным источником, который применяется к каждой подтаблице, развернутой в массиве.

**Примечания**

* В отличие от [`mv-expand`](./mvexpandoperator.md) оператора, `mv-apply` оператор поддерживает только расширение массива. Расширять контейнеры свойств не поддерживается.

**Примеры**

## <a name="getting-the-largest-element-from-the-array"></a>Получение самого крупного элемента из массива

<!-- csl: https://help.kusto.windows.net/Samples -->
```kusto
let _data =
range x from 1 to 8 step 1
| summarize l=make_list(x) by xMod2 = x % 2;
_data
| mv-apply element=l to typeof(long) on 
(
   top 1 by element
)
```

|`xMod2`|l           |element|
|-----|------------|-------|
|1    |[1, 3, 5, 7]|7      |
|0    |[2, 4, 6, 8]|8      |

## <a name="calculating-the-sum-of-the-largest-two-elements-in-an-array"></a>Вычисление суммы максимальных двух элементов в массиве

<!-- csl: https://help.kusto.windows.net/Samples -->
```kusto
let _data =
range x from 1 to 8 step 1
| summarize l=make_list(x) by xMod2 = x % 2;
_data
| mv-apply l to typeof(long) on
(
   top 2 by l
   | summarize SumOfTop2=sum(l)
)
```

|`xMod2`|l        |SumOfTop2|
|-----|---------|---------|
|1    |[1, 3, 5, 7]|12       |
|0    |[2, 4, 6, 8]|14       |


## <a name="using-with_itemindex-for-working-with-a-subset-of-the-array"></a>Использование `with_itemindex` для работы с подмножеством массива

<!-- csl: https://help.kusto.windows.net/Samples -->
```kusto
let _data =
range x from 1 to 10 step 1
| summarize l=make_list(x) by xMod2 = x % 2;
_data
| mv-apply with_itemindex=index element=l to typeof(long) on 
(
   // here you have 'index' column
   where index >= 3
)
| project index, element
```

|индекс|element|
|---|---|
|3|7|
|4|9|
|3|8|
|4|10|

## <a name="using-the-mv-apply-operator-to-sort-the-output-of-makelist-aggregate-by-some-key"></a>Использование `mv-apply` оператора для сортировки результатов `makelist` статистической обработки по некоторым ключам

<!-- csl: https://help.kusto.windows.net/Samples -->
```kusto
datatable(command:string, command_time:datetime, user_id:string)
[
    'chmod',        datetime(2019-07-15),   "user1",
    'ls',           datetime(2019-07-02),   "user1",
    'dir',          datetime(2019-07-22),   "user1",
    'mkdir',        datetime(2019-07-14),   "user1",
    'rm',           datetime(2019-07-27),   "user1",
    'pwd',          datetime(2019-07-25),   "user1",
    'rm',           datetime(2019-07-23),   "user2",
    'pwd',          datetime(2019-07-25),   "user2",
]
| summarize commands_details = make_list(pack('command', command, 'command_time', command_time)) by user_id
| mv-apply command_details = commands_details on
(
    order by todatetime(command_details['command_time']) asc
    | summarize make_list(tostring(command_details['command']))
)
| project-away commands_details
```

|`user_id`|`list_command_details_command`|
|---|---|
|user1|[<br>  "ls",<br>  "mkdir",<br>  "chmod",<br>  "Dir",<br>  "pwd",<br>  диспетчере<br>]|
|user2|[<br>  "RM",<br>  PWD<br>]|


**См. также:**

* Оператор [MV-Expand](./mvexpandoperator.md) .
