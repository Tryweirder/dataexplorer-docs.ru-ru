---
title: плагин поворота - Azure Data Explorer (ru) Документы Майкрософт
description: В этой статье описывается плагин с помощью azure Data Explorer.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 02/13/2020
ms.openlocfilehash: e4ec5a94483ade822280ee4a71106c214bb4b9a5
ms.sourcegitcommit: 47a002b7032a05ef67c4e5e12de7720062645e9e
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 04/15/2020
ms.locfileid: "81511107"
---
# <a name="pivot-plugin"></a>плагин поворота

Вращает таблицу, превращая уникальные значения из одной столбца в таблице ввода в несколько столбцов в таблице вывода и выполняет агрегации, в которых они необходимы на любых оставшихся значениях столбца, которые необходимы в конечном выходе.

```kusto
T | evaluate pivot(PivotColumn)
```

**Синтаксис**

`T | evaluate pivot(`*pivotКолонка*`[, `*агрегацииФункция*`] [,`*column1* `[,` *column2* ...`]])`

**Аргументы**

* *pivotКолонка*: Столбец для вращения. каждое уникальное значение из этой колонки будет столбцом в таблице вывода.
* *функция агрегирования:*(необязательно) агрегирует несколько строк в таблице ввода к одной строке в таблице вывода. В настоящее `min()` `max()`время `any()` `sum()`поддерживаемые функции: , , , `dcount()` `avg()`, , `stdev()`, `variance()`, и `count()` (по `count()`умолчанию).
* *column1*, *column2*, ...: (необязательно) названия столбцов. Таблица вывода будет содержать дополнительный столбец на каждый указанный столбец. по умолчанию: все столбцы, кроме поворотных столбцов и столбца агрегации.

**Возвращает**

Pivot возвращает вращаемую таблицу с указанными столбиками *(column1,* *column2,*...) плюс все уникальные значения поворотных столбцов. Каждая ячейка для поворотных столбцов будет содержать агрегированные вычисления функций.

**Примечание**

Схема вывода `pivot` плагина основана на данных, и поэтому запрос может производить различную схему для любых двух запусков. Это также означает, что запрос, отображающий неупакованные столбцы, может стать «сломанным» в любое время. По этой причине - не рекомендуется использовать этот плагин для автоматизации рабочих мест.

**Примеры**

### <a name="pivot-by-a-column"></a>Поворот по столбцу

Для каждого EventType и государств, начиная с 'AL', подсчитайте количество событий этого типа в этом состоянии.

```kusto
StormEvents
| project State, EventType 
| where State startswith "AL" 
| where EventType has "Wind" 
| evaluate pivot(State)
```

|EventType|АЛАБАМА|Аляска|
|---|---|---|
|Шквалистый ветер|352|1|
|Высокий ветер|0|95|
|Экстремальный холодный/Ветер Chill|0|10|
|Сильный ветер|22|0|


### <a name="pivot-by-a-column-with-aggregation-function"></a>Поворот столбцом с функцией агрегирования.

Для каждого EventType и государств, начиная с 'AR', отобразить общее количество прямых смертей.

```kusto
StormEvents 
| where State startswith "AR" 
| project State, EventType, DeathsDirect 
| where DeathsDirect > 0
| evaluate pivot(State, sum(DeathsDirect))
```

|EventType|Арканзас|Аризона|
|---|---|---|
|Сильный дождь|1|0|
|Шквалистый ветер|1|0|
|Молнии|0|1|
|Внезапное наводнение|0|6|
|Сильный ветер|1|0|
|Тепла|3|0|


### <a name="pivot-by-a-column-with-aggregation-function-and-a-single-additional-column"></a>Поворот столбцом с функцией агрегации и одним дополнительным столбцом.

Результат идентичен предыдущему примеру.

```kusto
StormEvents 
| where State startswith "AR" 
| project State, EventType, DeathsDirect 
| where DeathsDirect > 0
| evaluate pivot(State, sum(DeathsDirect), EventType)
```

|EventType|Арканзас|Аризона|
|---|---|---|
|Сильный дождь|1|0|
|Шквалистый ветер|1|0|
|Молнии|0|1|
|Внезапное наводнение|0|6|
|Сильный ветер|1|0|
|Тепла|3|0|


### <a name="specify-the-pivoted-column-aggregation-function-and-multiple-additional-columns"></a>Укажите поворотный столбец, функцию агрегирования и несколько дополнительных столбцов.

Для каждого типа события, источника и состояния суммируется количество прямых смертей.

```kusto
StormEvents 
| where State startswith "AR" 
| where DeathsDirect > 0
| evaluate pivot(State, sum(DeathsDirect), EventType, Source)
```

|EventType|Источник|Арканзас|Аризона|
|---|---|---|---|
|Сильный дождь|Специалист по управлению в чрезвычайных ситуациях|1|0|
|Шквалистый ветер|Специалист по управлению в чрезвычайных ситуациях|1|0|
|Молнии|Газета|0|1|
|Внезапное наводнение|Подготовленный корректировщик|0|2|
|Внезапное наводнение|Вещательные СМИ|0|3|
|Внезапное наводнение|Газета|0|1|
|Сильный ветер|Правоприменение|1|0|
|Тепла|Газета|3|0|