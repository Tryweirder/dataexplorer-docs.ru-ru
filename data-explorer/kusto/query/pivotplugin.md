---
title: подключаемый модуль Pivot — Azure обозреватель данных
description: В этой статье описывается подключаемый модуль Pivot в обозреватель данных Azure.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 02/13/2020
ms.openlocfilehash: d2f9db1dbace646c41d8751272cf44cf6d04c2c3
ms.sourcegitcommit: 09da3f26b4235368297b8b9b604d4282228a443c
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 07/28/2020
ms.locfileid: "87346139"
---
# <a name="pivot-plugin"></a>Подключаемый модуль pivot

Поворачивает таблицу путем преобразования уникальных значений из одного столбца входной таблицы в несколько столбцов в выходной таблице и выполняет агрегаты, где они требуются для всех оставшихся значений столбцов, которые нужны в конечном выводе.

```kusto
T | evaluate pivot(PivotColumn)
```

## <a name="syntax"></a>Синтаксис

`T | evaluate pivot(`*пивотколумн* `[, ` *aggregationFunction* `] [,` *Столбец1* `[,` *Столбец2* ...`]])`

## <a name="arguments"></a>Аргументы

* *пивотколумн*: столбец для поворота. каждое уникальное значение из этого столбца будет столбцом в выходной таблице.
* *статистическая функция*: (необязательно) объединяет несколько строк во входной таблице в одну строку в выходной таблице. Поддерживаемые в настоящее время функции: `min()` , `max()` , `any()` , `sum()` , `dcount()` , `avg()` , `stdev()` , `variance()` , `make_list()` , `make_bag()` , `make_set()` , `count()` (по умолчанию — `count()` ).
* *Столбец1*, *Столбец2*,... (необязательно) имена столбцов. Выходная таблица будет содержать дополнительный столбец для каждого указанного столбца. по умолчанию: все столбцы, кроме сведенного столбца и столбца агрегирования.

## <a name="returns"></a>Результаты

Функция Pivot возвращает повернутую таблицу с указанными столбцами (*Столбец1*, *Столбец2*,...) и все уникальные значения столбцов сведения. Каждая ячейка для сведенных столбцов будет содержать вычисление агрегатной функции.

**Примечание**

Выходная схема `pivot` подключаемого модуля основана на данных и, следовательно, запрос может создать другую схему для любого из двух запусков. Это также означает, что запрос, ссылающийся на распакованные столбцы, в любое время может стать "неразрывным". По этой причине не рекомендуется использовать этот подключаемый модуль для заданий службы автоматизации.

## <a name="examples"></a>Примеры

### <a name="pivot-by-a-column"></a>Сведение по столбцу

Для каждого объекта EventType и состояний, начинающихся с "AL", подсчитайте количество событий этого типа в этом состоянии.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
StormEvents
| project State, EventType 
| where State startswith "AL" 
| where EventType has "Wind" 
| evaluate pivot(State)
```

|EventType|АЛАБАМА|Аляска|
|---|---|---|
|Шквалистый ветер|352|1|
|Высокая обмотка|0|95|
|Экстремальный холодный/ветер подогрева|0|10|
|Строгая обмотка|22|0|


### <a name="pivot-by-a-column-with-aggregation-function"></a>Сведение по столбцу с статистической функцией.

Для каждого объекта EventType и состояний, начинающихся с "AR", отображается общее число прямых смерти.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
StormEvents 
| where State startswith "AR" 
| project State, EventType, DeathsDirect 
| where DeathsDirect > 0
| evaluate pivot(State, sum(DeathsDirect))
```

|EventType|КАК АРКАНЗАС так|ПОЯС|
|---|---|---|
|Тяжелая дождя|1|0|
|Шквалистый ветер|1|0|
|Молнии|0|1|
|Внезапное наводнение|0|6|
|Строгая обмотка|1|0|
|Температуры|3|0|


### <a name="pivot-by-a-column-with-aggregation-function-and-a-single-additional-column"></a>Сведение по столбцу с статистической функцией и одним дополнительным столбцом.

Результат идентичен предыдущему примеру.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
StormEvents 
| where State startswith "AR" 
| project State, EventType, DeathsDirect 
| where DeathsDirect > 0
| evaluate pivot(State, sum(DeathsDirect), EventType)
```

|EventType|КАК АРКАНЗАС так|ПОЯС|
|---|---|---|
|Тяжелая дождя|1|0|
|Шквалистый ветер|1|0|
|Молнии|0|1|
|Внезапное наводнение|0|6|
|Строгая обмотка|1|0|
|Температуры|3|0|


### <a name="specify-the-pivoted-column-aggregation-function-and-multiple-additional-columns"></a>Укажите сводный столбец, статистическую функцию и несколько дополнительных столбцов.

Для каждого типа события, источника и состояния Вычислите количество прямых смерти.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
StormEvents 
| where State startswith "AR" 
| where DeathsDirect > 0
| evaluate pivot(State, sum(DeathsDirect), EventType, Source)
```

|EventType|Source|КАК АРКАНЗАС так|ПОЯС|
|---|---|---|---|
|Тяжелая дождя|Специалист по управлению в чрезвычайных ситуациях|1|0|
|Шквалистый ветер|Специалист по управлению в чрезвычайных ситуациях|1|0|
|Молнии|Газета|0|1|
|Внезапное наводнение|Подготовленный корректировщик|0|2|
|Внезапное наводнение|Вещание мультимедиа|0|3|
|Внезапное наводнение|Газета|0|1|
|Строгая обмотка|Правоприменение|1|0|
|Температуры|Газета|3|0|
