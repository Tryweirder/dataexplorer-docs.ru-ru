---
title: series_decompose () — обозреватель данных Azure
description: В этой статье описывается series_decompose () в Azure обозреватель данных.
services: data-explorer
author: orspod
ms.author: orspodek
ms.reviewer: rkarlin
ms.service: data-explorer
ms.topic: reference
ms.date: 09/26/2019
ms.openlocfilehash: fa17d27506c4930fa9b9f7fb0a24f5ff31e1c974
ms.sourcegitcommit: 09da3f26b4235368297b8b9b604d4282228a443c
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 07/28/2020
ms.locfileid: "87345170"
---
# <a name="series_decompose"></a>series_decompose()

Применяет преобразование «декомпозиция» к ряду.  

Принимает выражение, содержащее ряд (динамический числовой массив) в качестве входных данных и разбирает его в сезонные, трендовые и остаточные компоненты.
 
## <a name="syntax"></a>Синтаксис

`series_decompose(`*Ряд* `[,` *Сезонности* `,` *Тенденция* `,` *Test_points* `,` *Seasonality_threshold*`])`

## <a name="arguments"></a>Аргументы

* *Ряд*: ячейка динамического массива, представляющая собой массив числовых значений, обычно результирующий результат операторов [make-Series](make-seriesoperator.md) или [make_list](makelist-aggfunction.md) .
* *Сезонности*: целое число, контролирующее сезонный анализ, содержащий либо
    * -1: автоматическое обнаружение сезонности с помощью [series_periods_detect](series-periods-detectfunction.md) (по умолчанию).
    * period: положительное целое число, определяющее ожидаемый период в количестве ячеек. Например, если ряд находится в ячейках по 1-h, еженедельный период составляет 168 ячеек.
    * 0: нет сезонности (пропустить извлечение этого компонента).    
* *Тенденция*. строка, управляющая анализом тенденций, содержащая одно из следующих значений:
    * "AVG": определение компонента тренда как среднего (x) (по умолчанию)
    * "линефит": извлечение компонента тренда с использованием линейной регрессии.
    * "None": нет тренда, пропустите извлечение этого компонента.    
* *Test_points*: 0 (по умолчанию) или положительное целое число, указывающее количество точек в конце последовательности, исключаемых из процесса обучения (регрессия). Этот параметр должен быть задан для целей прогнозирования.
* *Seasonality_threshold*: пороговое значение для оценки сезонности, когда для *сезонности* задано автоматическое обнаружение, пороговое значение оценки по умолчанию — `0.6` . Дополнительные сведения см. в разделе [series_periods_detect](series-periods-detectfunction.md).

**Вернул**

 Функция возвращает следующие соответствующие ряды:

* `baseline`: прогнозируемое значение ряда (суммарное количество сезонных и трендов компонентов, см. ниже).
* `seasonal`: серия сезонных компонентов:
    * Если точка не обнаружена или явно задана как 0: константа 0.
    * Если обнаружено или установлено положительное целое число, медиану точек ряда на одном этапе
* `trend`: серия компонента тренда.
* `residual`: серия остаточного компонента (то есть x-Baseline).
  

**Примечания**

* Порядок выполнения компонента:
    1. Извлеките сезонные серии
    2. Вычесть его из x, создавая последовательность
    3. Извлечение компонента тренда из временного ряда
    4. Создание базового = сезонного изменения
    5. Создание остатка = x-Baseline
    
* Необходимо включить сезонности и или тенденцию. В противном случае функция является избыточной и просто возвращает базовые показатели = 0 и остаточный = x.

**Дополнительные сведения о декомпозиции рядов**

Этот метод обычно применяется к временным рядам метрик, ожидаемым для периодического и (или) тренда в манифесте. Вы можете использовать метод для прогнозирования будущих значений метрик и (или) обнаружения аномальных значений. Неявное предположение этого процесса регрессии заключается в том, что в отличие от сезонных и трендов, временные ряды метод стохастическогося и распределяются случайным образом. Прогнозировать будущие значения метрик из компонентов сезонного и тренда, игнорируя остаточную часть. Обнаружение аномальных значений на основе обнаружения выбросов только на остаточной части. Дополнительные сведения можно найти в главе по [декомпозиции временных рядов](https://www.otexts.org/fpp/6).

## <a name="examples"></a>Примеры

**Еженедельный сезонности**

В следующем примере мы создаем серию с еженедельным сезонностиом и без тренда, а затем добавим в него некоторые выбросы. `series_decompose`находит и автоматически обнаруживает сезонности и формирует базовый уровень, почти идентичный сезонному компоненту. Добавленные выбросы можно четко увидеть в компоненте остатки.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
let ts=range t from 1 to 24*7*5 step 1 
| extend Timestamp = datetime(2018-03-01 05:00) + 1h * t 
| extend y = 2*rand() + iff((t/24)%7>=5, 10.0, 15.0) - (((t%24)/10)*((t%24)/10)) // generate a series with weekly seasonality
| extend y=iff(t==150 or t==200 or t==780, y-8.0, y) // add some dip outliers
| extend y=iff(t==300 or t==400 or t==600, y+8.0, y) // add some spike outliers
| summarize Timestamp=make_list(Timestamp, 10000),y=make_list(y, 10000);
ts 
| extend series_decompose(y)
| render timechart  
```

:::image type="content" source="images/samples/series-decompose1.png" alt-text="Разбиение рядов 1":::

**Еженедельное сезонности с тенденциями**

В этом примере мы добавим тенденцию к ряду из предыдущего примера. Сначала выполняется `series_decompose` с параметрами по умолчанию. Значение тренда `avg` по умолчанию принимает только среднее и не вычисляет тренд. Созданный базовый план не содержит тенденцию. При наблюдении за тенденциями в остатках он становится очевидным, что этот пример менее точен, чем в предыдущем примере.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
let ts=range t from 1 to 24*7*5 step 1 
| extend Timestamp = datetime(2018-03-01 05:00) + 1h * t 
| extend y = 2*rand() + iff((t/24)%7>=5, 5.0, 15.0) - (((t%24)/10)*((t%24)/10)) + t/72.0 // generate a series with weekly seasonality and ongoing trend
| extend y=iff(t==150 or t==200 or t==780, y-8.0, y) // add some dip outliers
| extend y=iff(t==300 or t==400 or t==600, y+8.0, y) // add some spike outliers
| summarize Timestamp=make_list(Timestamp, 10000),y=make_list(y, 10000);
ts 
| extend series_decompose(y)
| render timechart  
```

:::image type="content" source="images/samples/series-decompose2.png" alt-text="Разбиение ряда 2":::

Затем повторно запустите тот же пример. Так как мы ожидаем тренд в ряде, мы указываем `linefit` в параметре тенденция. Можно заметить, что обнаружена положительная тенденция, а базовая линия почти ближе к входному ряду. Остатки близки к нулю, и только выбросы выделены. В диаграмме можно увидеть все компоненты серии.

<!-- csl: https://help.kusto.windows.net:443/Samples -->
```kusto
let ts=range t from 1 to 24*7*5 step 1 
| extend Timestamp = datetime(2018-03-01 05:00) + 1h * t 
| extend y = 2*rand() + iff((t/24)%7>=5, 5.0, 15.0) - (((t%24)/10)*((t%24)/10)) + t/72.0 // generate a series with weekly seasonality and ongoing trend
| extend y=iff(t==150 or t==200 or t==780, y-8.0, y) // add some dip outliers
| extend y=iff(t==300 or t==400 or t==600, y+8.0, y) // add some spike outliers
| summarize Timestamp=make_list(Timestamp, 10000),y=make_list(y, 10000);
ts 
| extend series_decompose(y, -1, 'linefit')
| render timechart  
```

:::image type="content" source="images/samples/series-decompose3.png" alt-text="Разбиение ряда 3":::
